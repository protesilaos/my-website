---
layout: page
title: 'Emacs Lisp Elements'
subtitle: 'A big picture view of the Emacs Lisp programming language'
excerpt: "This book, written by Protesilaos Stavrou, also known as 'Prot', provides a big picture view of the Emacs Lisp programming language."
permalink: /emacs/emacs-lisp-elements
---

<p>
This book, written by Protesilaos Stavrou, also known as &ldquo;Prot&rdquo;,
provides a big picture view of the Emacs Lisp programming language.
</p>

<p>
The information furnished herein corresponds to stable version
2.0.0, released on 2025-12-10.
</p>

<ul class="org-ul">
<li>Official page: <a href="https://protesilaos.com/emacs/emacs-lisp-elements">https://protesilaos.com/emacs/emacs-lisp-elements</a></li>
<li>Git repository: <a href="https://github.com/protesilaos/emacs-lisp-elements">https://github.com/protesilaos/emacs-lisp-elements</a></li>
</ul>

<div id="table-of-contents" role="doc-toc">
<h2>Table of Contents</h2>
<div id="text-table-of-contents" role="doc-toc">
<ul>
<li><a href="#h:getting-started-with-emacs-lisp">1. Getting started with Emacs Lisp</a></li>
<li><a href="#h:basics-of-how-lisp-works">2. Basics of how Lisp works</a></li>
<li><a href="#h:evaluate-emacs-lisp">3. Evaluate Emacs Lisp</a></li>
<li><a href="#h:introspecting-emacs">4. Introspecting Emacs</a></li>
<li><a href="#h:side-effect-and-return-value">5. Side effect and return value</a></li>
<li><a href="#h:symbols-balanced-expressions-and-quoting">6. Symbols, balanced expressions, and quoting</a></li>
<li><a href="#h:buffers-as-data-structures">7. Buffers as data</a></li>
<li><a href="#h:text-has-its-own-properties">8. Text has its own properties</a></li>
<li><a href="#h:add-metadata-to-symbols">9. Add metadata to symbols</a></li>
<li><a href="#h:what-are-major-and-minor-modes">10. What are major and minor modes</a></li>
<li><a href="#h:hooks-and-the-advice-mechanism">11. Hooks and the advice mechanism</a></li>
<li><a href="#h:partial-evaluation-inside-of-a-list">12. Evaluate some elements inside of a list</a></li>
<li><a href="#h:evaluation-inside-of-a-macro-or-special-form">13. Evaluation inside of a macro or special form</a></li>
<li><a href="#h:mapping-through-a-list-of-elements">14. Mapping over a list of elements</a></li>
<li><a href="#h:the-match-data-of-the-last-search">15. The match data of the last search</a></li>
<li><a href="#h:switching-to-another-buffer-window-or-narrowed-state">16. Switching to another buffer, window, or narrowed state</a></li>
<li><a href="#h:basic-control-flow-with-if-cond-and-others">17. Basic control flow with <code>if</code>, <code>cond</code>, and others</a></li>
<li><a href="#h:control-flow-with-if-let-and-friends">18. Control flow with <code>if-let*</code> and friends</a></li>
<li><a href="#h:autoloading-symbols">19. Autoloading symbols</a></li>
<li><a href="#h:pattern-match-with-pcase-and-related">20. Pattern match with <code>pcase</code> and related</a></li>
<li><a href="#h:run-some-code-or-fall-back-to-some-other-code">21. Run some code or fall back to some other code</a></li>
<li><a href="#h:when-to-use-a-named-function-or-a-lambda-function">22. When to use a named function or a lambda function</a></li>
<li><a href="#h:make-your-interactive-function-also-work-from-lisp-calls">23. Make your interactive function also work from Lisp calls</a></li>
<li><a href="#h:emacs-as-a-computing-environment">24. Emacs as a computing environment</a></li>
<li><a href="#h:acknowledgements">25. Acknowledgements</a></li>
<li><a href="#h:copying">26. COPYING</a></li>
<li><a href="#h:gnu-free-documentation-license">27. GNU Free Documentation License</a></li>
<li><a href="#h:indices">28. Indices</a>
<ul>
<li><a href="#h:function-index">28.1. Function index</a></li>
<li><a href="#h:variable-index">28.2. Variable index</a></li>
<li><a href="#h:concept-index">28.3. Concept index</a></li>
</ul>
</li>
</ul>
</div>
</div>
<div id="outline-container-h:getting-started-with-emacs-lisp" class="outline-2">
<h2 id="h:getting-started-with-emacs-lisp"><span class="section-number-2">1.</span> Getting started with Emacs Lisp</h2>
<div class="outline-text-2" id="text-h:getting-started-with-emacs-lisp">
<p>
The purpose of this book is to provide you with a big picture view of
Emacs Lisp, also known as &ldquo;Elisp&rdquo; (<a href="#h:basics-of-how-lisp-works">Basics of how Lisp works</a>). This is
the programming language you use to extend Emacs. Emacs is a
programmable text editor: it interprets Emacs Lisp and behaves
accordingly. You can use Emacs without ever writing a single line of
code: it already has lots of features. Though you can, at any time,
program it to do exactly what you want by evaluating some Elisp that
either you wrote yourself or got from another person, such as in the
form of a package.
</p>

<p>
Programming your own text editor is both useful and fun. You can, for
example, streamline a sequence of actions you keep doing by combining
them into a single command that you then assign to a key binding: type
the key and&#x2014;bam!&#x2014;perform all the intermediate tasks in one go.
This makes you more efficient while it turns the editor into a
comfortable working environment.
</p>

<p>
The fun part is how you go about writing the code. There are no duties
you have to conform with. None! You program for the sake of
programming. It is a recreational activity that expands your horizons.
Plus, you cultivate your Elisp skills, which can prove helpful in the
future, should you choose to modify some behaviour of Emacs in a more
refined way.
</p>

<p>
Tinkering with Emacs is part of the experience. It teaches you to be
unapologetically opinionated about how your editor works. The key is
to know enough Elisp so that you do not spend too much time getting
frustrated because something trivial does not work. I am writing this
as a tinkerer myself with no background in computer science or
neighbouring studies: I learnt Emacs Lisp through trial and error by
playing around with the editor. My nominal goal was to improve certain
micro-motions I was repeating over and over. I sought efficiency only
to discover something much more profound and rewarding. Learning to
extend my editor has been a fulfilling experience and I am more
productive as a result. Emacs does what I want it to do and I am happy
with it.
</p>

<p>
Each chapter herein is generally short and to-the-point. Some are more
friendly to beginners while others dive deeper into advanced topics.
The progression from one chapter to the other should be linear, such
that you have enough information to proceed. There are links between
the chapters, exactly how a reference manual is supposed to be done.
You may then go back and forth to find what you need.
</p>

<p>
The text you find here is a combination of prose and code. The latter
may be actual Elisp or pseudo-code which captures the underlying
pattern. I encourage you to read this book either inside of Emacs or
with Emacs readily available. This way, you can play around with the
functions I give you, to further appreciate their nuances. The code I
write here is simple, trivial even. I want you to make sense of the
given principle and to not distract you with irrelevant complexity.
The goal is to show you how the pieces are put together. It is then up
to you to write elaborate programs.
</p>

<p>
The &ldquo;big picture view&rdquo; approach I am adopting is about covering the
concepts that I encounter frequently while working with Emacs Lisp.
This book is no substitute for the Emacs Lisp Reference Manual and
should by no means be treated as the source of truth for any of the
Elisp forms I comment on.
</p>

<p>
Good luck and enjoy!
</p>
</div>
</div>
<div id="outline-container-h:basics-of-how-lisp-works" class="outline-2">
<h2 id="h:basics-of-how-lisp-works"><span class="section-number-2">2.</span> Basics of how Lisp works</h2>
<div class="outline-text-2" id="text-h:basics-of-how-lisp-works">
<p>
Emacs Lisp is a programming language whose basic form has a simple
syntax: there are fundamental objects and lists of them. For example,
we have numbers and strings of characters inside double quotes (also
known as &ldquo;strings&rdquo;). These are some of the fundamental objects.
Symbols are another fundamental object: they are made up of
alphanumeric characters and/or symbols. They will often look like
ordinary words, though they are not inside a pair of double quotes,
otherwise they would be strings. A symbol is commonly used as the
proper name of some variable or function: they &ldquo;symbolise&rdquo; something
other than themselves. Though symbols can be self-referential, such
that they symbolise themselves. For example, the value of <code>nil</code> is
<code>nil</code>.
</p>

<p>
Every computation is expressed as a list. This is a list of
fundamental objects or, in other words, one or more fundamental
objects inside of a pair of parentheses. The first element of the list
is the symbol of the function. The remaining elements, if any, are the
arguments passed to the function. &ldquo;Arguments&rdquo; here means that they are
the exact values that correspond to the specified parameters of the
function. A function has &ldquo;parameters&rdquo;, meaning that it expects certain
inputs with which to carry out its work. Functions can define zero or
more parameters and internally decide how to handle them. Some
parameters can be mandatory and others optional. The number of
arguments must match that of the mandatory parameters, otherwise we
get an error.
</p>

<p>
Let us take a look at a basic function call. For example, this is how
we print a message, which we can then find in the <code>*Messages*</code> buffer:
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">(message "Hello world, this is my message to you!")
;; =&gt; "Hello world, this is my message to you!"
</pre>
</div>

<p>
Look at the syntax we have used. Here <code>message</code> is a symbol. It is the
first element of the list, therefore <code>message</code> is interpreted as the
proper name of a function. The second element of the list is a string
which, in this case, is understood as the first argument passed to the
<code>message</code> function. The entire expression is a list, hence the
parentheses around it. This expression is a function call: it performs
a computation.
</p>

<p>
When we use the term &ldquo;evaluate&rdquo; and derivatives, we are effectively
describing how we ask the computer to perform some computation for us,
such as to get the value of a variable or call a function for the work
it does and get back whatever it returns. In the above example, I
wrote the list and then evaluated it with C-x C-e
(<code>eval-last-sexp</code>). The text on the second line is my way of
representing the return value of this computation (<a href="#h:side-effect-and-return-value">Side effect and return value</a>).
</p>

<p>
What you just read explains most of Emacs Lisp. Though it can still be
difficult to understand a program because of how all the parentheses
line up. It can be hard to figure out where something starts and where
it ends when you have something like this:
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">(upcase (message "Hello world, the number is %s" (+ 1 1 1)))
;; =&gt; "HELLO WORLD, THE NUMBER IS 3"
</pre>
</div>

<p>
What we have above are three function calls inside of each other. If
you look at the closing three parentheses, you may panic as they are
already more than what you are used to. Relax! Elisp is cool because
you learn it by keeping calm. Let me read it to you from left to
right. Put the previous code block on one side and the following
explanation on the other, so that you can follow along more easily.
</p>

<ul class="org-ul">
<li>I am asked to evaluate this piece of Emacs Lisp.</li>

<li>I have found a parenthesis, which means that this is a list that
stands for a function call.</li>

<li>Let me check the first element of this list.</li>

<li>I found the symbol <code>upcase</code>. Fine, I will use this.</li>

<li>I know that <code>upcase</code> will work on a string to make all its letters
upper case (<a href="#h:introspecting-emacs">Introspecting Emacs</a>).</li>

<li>Now I will check the next element of this function call. It has to
be the argument passed to <code>upcase</code>.</li>

<li>The argument I have found starts with a parenthesis. This means that
it is not a fundamental object, but a function call in its own
right.</li>

<li>Before I do anything with <code>upcase</code>, I have to evaluate this function
call.</li>

<li>I found <code>message</code>, so I will use that. Time to check the next
element of this list.</li>

<li>I found the string. Good! It has a <code>%s</code> placeholder, which means
that there is another argument whose value will go where <code>%s</code> now
is.</li>

<li>Here is another parenthesis. We have a third function call.</li>

<li>The symbol I found here is <code>+</code>. This is a valid name for a function.
I will check what the arguments are.</li>

<li>I checked the arguments. They are all numbers. So I will apply <code>+</code>
to them.</li>

<li>I got the result of this function call, which is the number <code>3</code>. Now
I can go back to <code>message</code> with <code>3</code> as its second argument (the
first argument is that string I found which had the <code>%s</code>
placeholder). I have all the values to make it work.</li>

<li>Fine, I called <code>message</code>. It returns a string.</li>

<li>With this string I got back, I am finally ready to run <code>upcase</code>.</li>
</ul>

<p>
What I just described is how Emacs Lisp code is expressed in its
simplest form. This is enough to write a program.
</p>

<p>
Though you will eventually want to control when something is
evaluated. To this end, Emacs Lisp has the concept of quoting, which
is done with the single quote <code>'</code>. When the single quote is added as a
prefix to an object, then it means &ldquo;do not evaluate this right now and
take it as-is&rdquo; (<a href="#h:symbols-balanced-expressions-and-quoting">Symbols, balanced expressions, and quoting</a>). Using the
example from earlier with <code>message</code>:
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">(message "I got this: %s" '(one two three))
;; =&gt; "I got this: (one two three)"
</pre>
</div>

<p>
When Emacs reads this, it now finds only one function call, which
starts with the opening parenthesis for <code>message</code>. Then it reads one
list of data, which starts with the quoted list of symbols we have
there. By using the quote, we have successfully controlled where
evaluation should happen and where it should just take what we give it
without trying to interpret it.
</p>

<p>
Remember that when we use &ldquo;quote&rdquo; and related words in Emacs Lisp, we
are referring to this single quote, which is always a prefix. We do
not have pairs of single quotes around words or lists. No. It is only
a prefix for the object that follows.
</p>

<p>
If we did not have the single quote there, then we would effectively
be telling Emacs to evaluate <code>(one two three)</code> as a function call
where <code>one</code> would be the name of the function and the rest would be
its arguments. Unless you have a function whose proper name is <code>one</code>,
this will result in an error.
</p>

<p>
Earlier in this chapter I wrote &ldquo;every computation is expressed as a
list&rdquo;. When you are writing Emacs Lisp, you may find that this does
not seem to be the case. For example, you can have a variable that you
evaluate on its own. Let us first declare a variable and give it a
string value:
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">(defvar my-name "Protesilaos")
;; =&gt; my-name
</pre>
</div>

<p>
We have now defined the symbol <code>my-name</code>, whose value is the string of
characters <code>Protesilaos</code>. What you read here looks exactly like the
function calls we discussed in the previous examples. This is what
Lisp is all about. Now you may wonder what happens if we evaluate
<code>my-name</code> without doing a function call. Like this:
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">my-name
;; =&gt; "Protesilaos"
</pre>
</div>

<p>
We get back the value. So is what I wrote earlier about everything
being a list incorrect? While you do not have any parentheses there,
it is helpful to think that Emacs makes things easier for you by
internally using the <code>symbol-value</code> function to interpret the symbol
<code>my-name</code>:
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">(symbol-value 'my-name)
;; =&gt; "Protesilaos"
</pre>
</div>

<p>
Note that the <code>symbol-value</code> here is important because it is the
function that takes the <code>my-name</code> symbol as an argument. But you
cannot evaluate a variable by writing it as a function call. This is
erroneous:
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">(my-name)
;; =&gt; ERROR...
</pre>
</div>

<p>
The error pertains to the fact that <code>my-name</code> is not a function: it is
a variable. Emacs has different namespaces for functions and
variables, meaning that you can have <code>my-name</code> as a symbol of a
function and as the symbol of a variable. Those are two separate
objects that we distinguish by how they are called. Let me show you:
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">(defun my-name nil
  "I am the function that returns this string which includes Protesilaos")
;; =&gt; my-name
</pre>
</div>

<p>
Again, you notice in the code above the idea of everything being a
list: <code>defun</code> is what we call to interpret all the other elements. We
now have defined <code>my-name</code> as a function. All this function does is
return a string, which in practice is not really useful: we could have
done this with a variable. Though what we are interested in at present
is to understand how we can have <code>my-name</code> be both a variable and a
function which we tell apart based on how they are used in context. It
is time to test our work:
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">;; Here I am evaluating the variable.  When you evaluate a variable
;; directly, it is helpful to think that internally Emacs translates
;; your request to something like this: (symbol-value 'my-name)
my-name
;; =&gt; "Protesilaos"

;; Here I am evaluating the function.
(my-name)
;; =&gt; "I am the function that returns this string which includes Protesilaos"
</pre>
</div>

<p>
What we have discussed thus far should help you make sense of all
these nested parentheses you will be dealing with when working with
Emacs Lisp. As you gain more experience, you will discover cases where
the syntax varies slightly (<a href="#h:basic-control-flow-with-if-cond-and-others">Basic control flow with <code>if</code>, <code>cond</code>, and others</a>).
This is because programmers want to economise on typing the same
things over and over, so they design &ldquo;special forms&rdquo; or &ldquo;Lisp macros&rdquo;
that handle some things internally (<a href="#h:evaluation-inside-of-a-macro-or-special-form">Evaluation inside of a macro or special form</a>).
Specifically, you will encounter cases where quoting seems to be
different than what you have learnt here. Worry not. The principles are
the same. Those exceptions will ultimately be easy to reason about.
</p>

<p>
To avoid complex language such as &ldquo;a list of list of lists with quoted
lists and fundamental objects&rdquo; we use the term &ldquo;expression&rdquo;. In
general, an expression is anything that Emacs can evaluate, be it a
list, list of lists, or symbol (<a href="#h:symbols-balanced-expressions-and-quoting">Symbols, balanced expressions, and quoting</a>).
</p>
</div>
</div>
<div id="outline-container-h:evaluate-emacs-lisp" class="outline-2">
<h2 id="h:evaluate-emacs-lisp"><span class="section-number-2">3.</span> Evaluate Emacs Lisp</h2>
<div class="outline-text-2" id="text-h:evaluate-emacs-lisp">
<p>
Everything you do in Emacs calls some function (<a href="#h:basics-of-how-lisp-works">Basics of how Lisp works</a>).
Click a button? That is a function. Write a few words? That is a
series of functions. Use the arrow keys to move around? Functions;
functions all the way down! Emacs evaluates Emacs Lisp code, reading
the return values while dealing with side effects (<a href="#h:side-effect-and-return-value">Side effect and return value</a>).
</p>

<p>
The fact that a computer program does computation is not peculiar to
Emacs. What is special is the combination of two qualities: (i) Emacs
is free software and (ii) it is extended in the same programming
language it is written in. Its inherent freedom means that every
single line of code that is evaluated is available to you for
potential inspection, modification, and redistribution (<a href="#h:introspecting-emacs">Introspecting Emacs</a>).
</p>

<p>
Nothing is hidden from you. While the uniform extensibility
through Emacs Lisp means that the code you are reading and the code
you are writing are treated the same way. You do not need to learn
different, typically incompatible, programming languages or
configuration formats to make Emacs perform the computations you
want. Emacs Lisp is all there is.
</p>

<p>
Freedom and uniformity of computation together give you maximum
control over your text editor (<a href="#h:emacs-as-a-computing-environment">Emacs as a computing environment</a>). The
promise, then, is that learning to program in Elisp grants you access
to the full power of Emacs, such that you do exactly what you want
with it, given that you also understand some basic concepts such as
buffers and text manipulation (<a href="#h:buffers-as-data-structures">Buffers as data</a>).
</p>

<p>
You type a key on your keyboard and a character is written to the
current buffer at the point where the cursor is. This is a function
bound to a key. It actually is an interactive function, because you
are calling it via a key binding rather than through some other Emacs
Lisp program (the latter is also described as &ldquo;calling it from Lisp&rdquo;
or &ldquo;Lisp call&rdquo;). Interactive functions are known as &ldquo;commands&rdquo;. They
are computations which we can invoke interactively on demand.
</p>

<p>
Though do not let the implementation detail of interactivity distract
you from the fact that every single action you perform in Emacs
involves the evaluation of Emacs Lisp. A command will typically rely
on the computations of other functions to do its work. For example, if
I have a function that increments a list of numbers, I am internally
calling another function for adding one number to another, and then
yet another function to apply this operation over each element of the
list (<a href="#h:mapping-through-a-list-of-elements">Mapping over a list of elements</a>).
</p>

<p>
Apart from direct key bindings, the other pattern of interaction is
with the M-x (<code>execute-extended-command</code>) key: it produces
a minibuffer prompt that asks you to select a command by its name. You
press the RET key and Emacs proceeds to execute the command
you selected. What you get in this prompt is a list of all the
commands that have currently been evaluated or automatically loaded
(<a href="#h:autoloading-symbols">Autoloading symbols</a>).
</p>

<p>
Emacs can evaluate Elisp code from anywhere. Normally, you would want
to do this in a buffer whose major mode is designed for editing Emacs
Lisp (<a href="#h:what-are-major-and-minor-modes">What are major and minor modes</a>). But Emacs will blithely
evaluate any Elisp expression from any buffer, including those whose
major mode is not designed to treat Elisp code in some special way. If
you have some Elisp in your buffer, you can place the cursor at the
end of its closing parenthesis and type C-x C-e
(<code>eval-last-sexp</code>). Similarly, you can use the commands <code>eval-buffer</code>
and <code>eval-region</code> to operate on the current buffer or highlighted
region, respectively. The <code>eval-buffer</code> only makes sense in a buffer
that contains only Elisp, otherwise you will probably get errors from
asking Emacs to evaluate text that does not correspond to known
functions or variables and whose syntax does not consist of lists.
</p>

<p>
To evaluate some code is to make its results available to the running
Emacs session. You are, in effect, teaching Emacs something new. It
will know about it for as long as the session is active and you have
not overwritten this knowledge, as it were, with some new definition
of it. This is how you add new functionality to Emacs, redefine
existing one, or simply get the value out of some code you are using.
</p>

<p>
The <code>eval-last-sexp</code> also works on symbols (<a href="#h:symbols-balanced-expressions-and-quoting">Symbols, balanced expressions, and quoting</a>).
For example, if you place the cursor at the end of the variable
<code>buffer-file-name</code> and use C-x C-e (<code>eval-last-sexp</code>), you
will get the value of that variable, which is either <code>nil</code> or the file
system path to the file you are editing in the current buffer.
Internally, it is like evaluating <code>(symbol-value 'buffer-file-name)</code>,
as we covered in the chapter about the basics of Lisp (<a href="#h:basics-of-how-lisp-works">Basics of how Lisp works</a>).
</p>

<p>
Sometimes the above are not appropriate for what you are trying to do.
Suppose you intend to write a command that copies the file path of the
current buffer. To do that, you need your code to test the value of
the variable <code>buffer-file-name</code> (<a href="#h:buffers-as-data-structures">Buffers as data structures</a>). But you
do not want to type out <code>buffer-file-name</code> in your actual file, then
use one of the aforementioned commands for Elisp evaluation, and then
undo your edits. Such is a cumbersome way of doing what is needed,
plus it is prone to mistakes! The best way to run Elisp in the current
buffer is to type M-: (<code>eval-expression</code>): it opens the
minibuffer and expects you to write the code you want to evaluate.
Type RET from there to proceed. The evaluation is done with
the last buffer as current (the buffer that was current prior to
calling <code>eval-expression</code>). This is especially useful if you want to
do a quick test of some part of a bigger procedure you are working on.
</p>

<p>
Here is some Emacs Lisp you may want to try in (i) a buffer that
corresponds to a file versus (ii) a buffer that is not associated with
any file on disk, such as the <code>*scratch*</code> buffer.
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">;; Use `eval-expression' to evaluate this code in a file-visiting
;; buffer versus a buffer that does not visit any file.
(if buffer-file-name
    (message "The path to this file is `%s'" buffer-file-name)
  (message "Sorry mate, this buffer is not visiting a file"))
</pre>
</div>

<p>
When you are experimenting with code, you want to test how it behaves
and confirm you are getting the expected results out of it. Since Lisp
consists of lists that can feed into each other, you can evaluate
smaller snippets of code while working towards the final version of
the program. Use the command <code>ielm</code> to open an interactive shell for
Emacs Lisp evaluation. It puts you at a prompt where you can type any
Elisp and hit RET to evaluate it. The return value is
printed right below.
</p>

<p>
Alternatively, switch to the <code>*scratch*</code> buffer. If it is using the
major mode <code>lisp-interaction-mode</code>, which is the default value of the
variable <code>initial-major-mode</code>, then you can move around freely in that
buffer and type C-j (<code>eval-print-last-sexp</code>) at the end of
some code to evaluate it. This works almost the same way as
<code>eval-last-sexp</code>, with the added effect of putting the return value
right below the expression you just evaluated.
</p>

<p>
In addition to these, you can rely on the self-documenting nature of
Emacs to figure out what the current state is. For example, to learn
about the buffer-local value of the variable <code>major-mode</code>, you can do
C-h v (<code>describe-variable</code>), and then search for that
variable. The resulting <code>*Help*</code> buffer will inform you about the
current value of <code>major-mode</code>. This help command and many others like
<code>describe-function</code>, <code>describe-keymap</code>, <code>describe-key</code>, and
<code>describe-symbol</code>, provide insight into what Emacs knows about a given
object. The <code>*Help*</code> buffer will show relevant information, such as
the path to the file that defines the given function or whether a
variable is declared as buffer-local (<a href="#h:introspecting-emacs">Introspecting Emacs</a>).
</p>
</div>
</div>
<div id="outline-container-h:introspecting-emacs" class="outline-2">
<h2 id="h:introspecting-emacs"><span class="section-number-2">4.</span> Introspecting Emacs</h2>
<div class="outline-text-2" id="text-h:introspecting-emacs">
<p>
Emacs is &ldquo;self-documenting&rdquo; because it is aware of its state and can
report on it. When you define a variable, such as <code>my-name</code>, Emacs
retains in its running memory the symbol <code>my-name</code> and its
corresponding value of, say, <code>Protesilaos</code>. If you invoke the command
<code>describe-variable</code>, it will produce a minibuffer prompt from where
you can select <code>my-name</code> among all other known variables. Once you
select that, a <code>*Help*</code> buffer will be displayed. There you will find
information about <code>my-name</code>, such as its current value, any
documentation it may have and whether it has a buffer-local value
(<a href="#h:buffers-as-data-structures">Buffers as data structures</a>).
</p>

<p>
At all times, you update the state of Emacs by evaluating Elisp code
(<a href="#h:evaluate-emacs-lisp">Evaluate Emacs Lisp</a>). Let us consider the example of a variable that
we define for the first time:
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">(defvar my-name "Protesilaos"
  "This is some documentation about my name.
A good Elisp program will document all the functions and variables it
provides.  Emacs developers have a strong documentation culture.  It
complements the software freedom we stand for, because it empowers users
to learn from the tools available to them and thus exercise their
freedom.")
</pre>
</div>

<p>
We use the special form <code>defvar</code> to declare <code>my-name</code> with an initial
value of <code>Protesilaos</code> as a string (<a href="#h:evaluation-inside-of-a-macro-or-special-form">Evaluation inside of a macro or special form</a>).
The second line specifies the documentation of this variable.
Documentation strings, also known as &ldquo;docstrings&rdquo; are optional though
strongly recommended as a matter of convention. As you can tell from
the sample above, docstrings are how we make software freedom more
accessible to users. Like me, they can learn by reading this
information and then by trying to understand the code.
</p>

<p>
A combination of consistent reading habits and the ease of trial and
error that Emacs provides makes it possible to learn Elisp by doing
more with it, one small step at a time. This is how every Emacs user
can learn to program in Emacs Lisp or, at least, better understand
what their tool is doing. Such is the realisation of the promise of
free software: everyone, not just professional programmers, benefits
from it. And is why, in my opinion, Emacs epitomises the stated
objectives of the GNU project for software freedom: it incorporates
them and puts them to practice, all in one powerful program.
</p>

<p>
Once we evaluate the <code>defvar</code> for <code>my-name</code>, <code>describe-variable</code> will
produce a <code>*Help*</code> buffer with the following contents about it:
</p>

<pre class="example" id="org8431957">
my-nameâ€™s value is "Protesilaos"

This is some documentation about my name.
A good Elisp program will document all the functions and variables it
provides.  Emacs developers have a strong documentation culture.  It
complements the software freedom we stand for, because it empowers users
to learn from the tools available to them and thus exercise their
freedom.
</pre>

<p>
Now go to the source code of <code>my-name</code> and modify its value to <code>Prot</code>
between double quotes. Then invoke the command <code>eval-defun</code> to
evaluate the definition anew. If you now call <code>describe-variable</code>, you
will get a <code>*Help*</code> buffer with the updated value. This is how Emacs
works for all symbols it has evaluated. When the source code is in a
file, the <code>*Help*</code> buffer will include a link to it. If you find such
a link, click on it to navigate to the file and at the locus where the
source code is.
</p>

<p>
Depending on how the code is written, Emacs will have access to
metadata that can feed into those <code>*Help*</code> buffers, among other uses.
For example, when a variable is defined, it can specify its original
value (<a href="#h:add-metadata-to-symbols">Add metadata to symbols</a>). What matters at this point is that
Emacs will always know what the current state is. Make a habit out of
using the commands <code>describe-variable</code>, <code>describe-function</code>,
<code>describe-keymap</code>, <code>describe-key</code>, and <code>describe-symbol</code>, among
others, whenever you have a question about what Emacs knows.
</p>

<p>
Do this before you search for further information online: it will
teach you to rely more on the primary source of Emacs and less on
secondary sources on the Internet. Plus, the resulting <code>*Help*</code>
buffers will link you to the source code, from where you may learn
something more than just what you are searching for, such as how a
certain function is autoloaded (<a href="#h:autoloading-symbols">Autoloading symbols</a>). This is how you
build up your capacity for working with Elisp, which is how you get to
extend Emacs the way you want.
</p>
</div>
</div>
<div id="outline-container-h:side-effect-and-return-value" class="outline-2">
<h2 id="h:side-effect-and-return-value"><span class="section-number-2">5.</span> Side effect and return value</h2>
<div class="outline-text-2" id="text-h:side-effect-and-return-value">
<p>
Emacs Lisp has functions. They take inputs and produce outputs. In its
purest form, a function is a computation that only returns a value: it
does not change anything in its environment. The return value of a
function can be used as input for another function, in what
effectively is a chain of computations. You can thus rely on a
function&rsquo;s return value to express something like &ldquo;if this works the
way I expect, then also do this other thing, otherwise do something
else or even nothing&rdquo;.
</p>

<p>
Elisp is the language that extends and controls Emacs. This means that
it also affects the state of the editor. When you run a function, it
can make permanent changes, such as to insert some text at the point
of the cursor, delete a buffer, create a new window, permanently
change the value of some variable or function, and so on. These
changes may have an impact on future computations. For example, if the
previous function deleted a certain buffer, the next function which
was supposed to write to that same buffer can no longer do its job:
the buffer is gone! We call all these changes to the environment &ldquo;side
effects&rdquo;. They are the byproducts of the computation, which itself
results in some value, i.e. the &ldquo;return value&rdquo;.
</p>

<p>
When you write Elisp, you have to account for both the return value
and the side effects. If you are sloppy, you will get unintended
results caused by all those ill-considered changes to the environment.
But if you use side effects meticulously, you use Elisp to its full
potential. For instance, imagine you define a function that follows
the logic of &ldquo;create a buffer, go there, write some text, save the
buffer to a file at my preferred location, and then come back where I
was before I called this function, while leaving the created buffer
open&rdquo;. All these are side effects and they are all instrumental to
your goal. Your function may have some meaningful return value as well
that you can employ as the input of another function. For example,
your function could return the buffer object it generated, so that the
next function can do something useful with it like display that buffer
in a separate frame and make its text larger.
</p>

<p>
The idea is to manipulate the state of the editor, to make Emacs do
what you envision. Sometimes this means your code has side effects. At
other times, side effects are useless or even run counter to your
intended patterns of behaviour. You will keep refining your intuition
about what needs to be done as you gain more experience and expand the
array of your skills (<a href="#h:symbols-balanced-expressions-and-quoting">Symbols, balanced expressions, and quoting</a>). No
problem; no stress!
</p>
</div>
</div>
<div id="outline-container-h:symbols-balanced-expressions-and-quoting" class="outline-2">
<h2 id="h:symbols-balanced-expressions-and-quoting"><span class="section-number-2">6.</span> Symbols, balanced expressions, and quoting</h2>
<div class="outline-text-2" id="text-h:symbols-balanced-expressions-and-quoting">
<p>
To someone not familiar with Emacs Lisp, it is a language that has so
many nested parentheses (<a href="#h:basics-of-how-lisp-works">Basics of how Lisp works</a>)! Here is a simple
function definition:
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">(defun my-greet-person (name)
  "Say hello to the person with NAME."
  (message "Hello %s" name))
</pre>
</div>

<p>
I just defined the function with the name <code>my-greet-person</code>. It has a
list of parameters, specifically, a list of one parameter, called
<code>name</code>. Then is the optional documentation string, which is for users
to make sense of the code and/or understand the intent of the function
(<a href="#h:introspecting-emacs">Introspecting Emacs</a>).
</p>

<p>
By convention, we write in the imperative voice (&ldquo;Say hello and do
THIS&rdquo; instead of &ldquo;Says hello and does THIS&rdquo;) and use upper case
letters to refer to the parameters or something that works as a
placeholder. Even in that parenthetic comment I made about the
imperative voice I followed the convention of using all capital
letters to denote a variable/placeholder with <code>THIS</code>. Looks neat!
</p>

<p>
<code>my-greet-person</code> takes <code>name</code> as input and passes it to the function
<code>message</code> as an argument to ultimately print a greeting. The <code>message</code>
function returns the string it formats and also produces the side
effect of appending it to the <code>*Messages*</code> buffer. You can visit the
<code>*Messages*</code> buffer directly with C-h e (<code>view-echo-area-messages</code>).
</p>

<p>
At any rate, this is how you call <code>my-greet-person</code> with the one
argument it expects:
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">(my-greet-person "Protesilaos")
</pre>
</div>

<p>
Now do the same with more than one parameters:
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">(defun my-greet-person-from-country (name country)
  "Say hello to the person with NAME who lives in COUNTRY."
  (message "Hello %s of %s" name country))
</pre>
</div>

<p>
And call it thus:
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">(my-greet-person-from-country "Protesilaos" "Cyprus")
</pre>
</div>

<p>
Even for the most basic tasks, you have parentheses everywhere: the
uniformity of Lisp means that we have lists all the way down! These
actually establish a clear structure, which make it easier to reason
about your program. If Lisp does not feel easy right now, it is
because you are not used to it yet. Once you do, there is no going
back.
</p>

<p>
The basic idea of any dialect of Lisp, Emacs Lisp being one of them,
is that you have parentheses which delimit lists. A list consists of
elements. Lists are either evaluated to produce the results of some
computation or returned as they are for use in some other evaluation
(<a href="#h:side-effect-and-return-value">Side effect and return value</a>):
</p>

<dl class="org-dl">
<dt>The list as a function call</dt><dd>When a list is evaluated, the first
element is the name of the function and the remaining elements are
the arguments passed to it. You already saw this play out above with
how I called <code>my-greet-person</code> with <code>"Protesilaos"</code> as its argument.
Same principle for <code>my-greet-person-from-country</code>, with
<code>"Protesilaos"</code> and <code>"Cyprus"</code> as its arguments.</dd>

<dt>The list as data</dt><dd>When a list is not evaluated, then none of its
elements has any special meaning at the outset. They are all
returned as a list of elements without further changes. When you do
not want your code to be evaluated, you prefix it with a single
quote character. For example, <code>'("Protesilaos" "Prot" "Cyprus")</code> is
a list of three elements that should be returned as-is. The quote
prefix can be applied to symbols as well, such as <code>(symbol-function
  'my-greet-person)</code> to get the actual value out of the function
object whose symbol is <code>my-greet-person</code> (<a href="#h:basics-of-how-lisp-works">Basics of how Lisp works</a>).</dd>
</dl>

<p>
Consider the latter case of lists as data, which you have not seen
yet. You have a list of elements and you want to get something out of
it. At the most basic level, the functions <code>car</code> and <code>cdr</code> return the
first element and the list of all remaining elements, respectively:
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">(car '("Protesilaos" "Prot" "Cyprus"))
;; =&gt; "Protesilaos"

(cdr '("Protesilaos" "Prot" "Cyprus"))
;; =&gt; ("Prot" "Cyprus")
</pre>
</div>

<p>
The single quote here is critical, because it instructs Emacs to not
evaluate the list. Otherwise, the evaluation of this list would treat
the first element, namely <code>"Protesilaos"</code>, as the name of a function
and the remainder of the list as the arguments to that function. As
you do not have the definition of such a function, you get an error.
</p>

<p>
Certain data types in Emacs Lisp are &ldquo;self-evaluating&rdquo;. This means
that if you evaluate them, their return value is what you are already
working with. For example, the return value of the string of
characters <code>"Protesilaos"</code> is <code>"Protesilaos"</code>. This is true for
strings, numbers, keywords, symbols, and the special <code>nil</code> or <code>t</code>.
Here is a list with a sample of each of these, which you construct by
calling the function <code>list</code>:
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">(list "Protesilaos" 1 :hello 'my-greet-person-from-country nil t)
;; =&gt; ("Protesilaos" 1 :hello my-greet-person-from-country nil t)
</pre>
</div>

<p>
The <code>list</code> function evaluates the arguments passed to it, unless they
are quoted. The reason you get the return value without any apparent
changes is because of self-evaluation. Notice that <code>my-greet-person-from-country</code>
is quoted the same way we quote a list: we do not want to evaluate at
this moment, but only to include its symbol in the resulting list.
Without it, <code>my-greet-person-from-country</code> would be evaluated, which
would return an error of a void/undefined variable, unless that symbol
was also defined as a variable.
</p>

<p>
Pay close attention to the above code block and the resulting value I
included in the comment below. You will notice that <code>my-greet-person-from-country</code>
was quoted but is not quoted anymore. You can do this with any symbol
or list (<a href="#h:evaluate-emacs-lisp">Evaluate Emacs Lisp</a>). Every time you evaluate a quote
expression, you get back the expression without the quote. This means
that if you evaluate it again, you will get something different, such
as a function call, the value of a variable, or some error.
</p>

<p>
As such, it is helpful to think of the single quote prefix as an
instruction to &ldquo;do not evaluate this right now, but pass it over to
the next evaluation&rdquo;. In its simplest form though, you can just say
&ldquo;do not evaluate the following&rdquo;, which is exactly what happens. More
specifically, it is an instruction to not perform evaluation if it
would have normally happened in that context (<a href="#h:partial-evaluation-inside-of-a-list">Evaluate some elements inside of a list</a>).
</p>

<p>
In other words, you do not want to quote something inside of a quoted
list, because that is the same as quoting it twice. The way the quote
prefix works is that it passes the &ldquo;do not evaluate&rdquo; instruction to
its entire form, which in the case of a list is from one parenthesis
all the way to the closing matching parenthesis, covering everything
in between.
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">;; This is the correct way:
'(1 :hello my-greet-person-from-country)

;; It is wrong to quote `my-greet-person-from-country' because the
;; entire list would not have been evaluated anyway.  The mistake here
;; is that you are quoting what is already quoted, like doing
;; ''my-greet-person-from-country.
'(1 :hello 'my-greet-person-from-country)
</pre>
</div>

<p>
Now you may be wondering why did we quote <code>my-greet-person-from-country</code>
but nothing else? The reason is that everything else you saw there is
effectively &ldquo;self-quoting&rdquo;, i.e. the flip-side of self-evaluation.
Whereas <code>my-greet-person-from-country</code> is a symbol. A &ldquo;symbol&rdquo; is
usually a reference to something other than itself: it either
represents some computation&#x2014;a function&#x2014;or the value of a variable.
The symbols <code>nil</code> and <code>t</code> are special as they always refer to
themselves, meaning that when you evaluate them you get back exactly
what you evaluated (<a href="#h:add-metadata-to-symbols">Add metadata to symbols</a>).
</p>

<p>
If you write a symbol without quoting it, you are effectively telling
Emacs &ldquo;give me the value this symbol represents&rdquo;. In the case of
<code>my-greet-person-from-country</code>, you will get an error if you try that
because this symbol is not a variable and thus trying to get a value
out of it (the <code>symbol-value</code> to be precise) is not going to work.
</p>

<p>
To make it easier to describe our actions, we say that we are
evaluating &ldquo;expressions&rdquo;. These are the objects that Emacs can
evaluate, such as a list, a list of lists, or fundamental objects in
their own right, like strings and symbols. These expressions need to
be balanced, meaning that either they are self-contained (e.g. a
string) or have parentheses around them and start and end where they
are supposed to be.
</p>

<p>
A common error in Elisp programs is to have unbalanced parentheses,
such as by forgetting one parenthesis at the end or mistyping an extra
one. Some programmers use packages that automatically balance Elisp
code. The built-in <code>electric-pair-mode</code> minor mode can help in this
regard (<a href="#h:what-are-major-and-minor-modes">What are major and minor modes</a>). It automatically inserts
delimiters as pairs, so you will probably have the correct number of
parentheses each time. You may like to write code this way. Though it
is a matter of style. I am among those who prefer to do things
manually. I find such &ldquo;electric&rdquo; behaviour to be more trouble than it
is worth, given how I like to move around in a buffer. Perhaps, then,
the name &ldquo;electric&rdquo; draws from the ability of this mode to shock you
at times.
</p>

<p>
Keep in mind that Emacs Lisp has a concept of &ldquo;macro&rdquo;, which basically
is a templating system to write code that actually expands into some
other code which is then evaluated. Macros are not some other language
with its own syntax. They are Emacs Lisp as well, only they make more
advanced use of quoting to control how evaluation is done and where.
Inside of a macro, you specify what is evaluated now as opposed to
later, meaning that the aforementioned may not apply directly to calls
that involve the macro, even if they are still used inside of the
macro&rsquo;s expanded form (<a href="#h:evaluation-inside-of-a-macro-or-special-form">Evaluation inside of a macro or special form</a>).
</p>

<p>
As you expose yourself to more Emacs Lisp code, you will encounter
quotes that are preceded by the hash sign, like <code>#'some-symbol</code>. This
&ldquo;sharp quote&rdquo;, as it is called, is the same as the regular quote with
the added semantics of referring to a function in particular. The
programmer can thus better articulate the intent of a given
expression, while the byte compiler may internally perform the
requisite checks and optimisations.
</p>

<p>
This is not merely a matter of semantics though. When you define a
symbol, there is a difference between storing in it a value or a
closure. A &ldquo;closure&rdquo; is an object consisting of a <code>lambda</code> with its
parameters and the body of form. In other words, defining a variable
and defining a value leads to a different result. Consider how the
functions <code>symbol-value</code> and <code>symbol-function</code> work: they both examine
a symbol. <code>symbol-value</code> treats the symbol as a variable and will
return its value, if any. <code>symbol-function</code> treats the symbol as a
function and returns its closure, if any.
</p>

<p>
Here is how it works:
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">(defvar my-name "Protesilaos")
;; =&gt; my-name

(symbol-value 'my-name)
;; =&gt; "Protesilaos"

(symbol-function 'my-name)
;; =&gt; nil

(defun my-greet (person)
  (format "Hello %s" person))
;; =&gt; my-greet

(symbol-function 'my-greet)
;; =&gt; #[(person) ((format "Hello %s" person)) nil]

(symbol-value 'my-greet)
;; =&gt; ERROR...
</pre>
</div>

<p>
Notice that <code>[(person) ((format "Hello %s" person)) nil]</code> is the
printed representation of a closure. This is the object corresponding
to the following:
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">(lambda (person)
  (format "Hello %s" person))
</pre>
</div>

<p>
In fact, when you use <code>defun</code> it actually creates an alias for such a
lambda. Place the cursor at the closing parenthesis of the <code>defun</code>
expression and invoke the command <code>pp-macroexpand-last-sexp</code> to
confirm as much. I am printing it below for your convenience:
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">(defun my-greet (person)
  (format "Hello %s" person))
;; =&gt; (defalias 'my-greet (lambda (person) (format "Hello %s" person)))
</pre>
</div>

<p>
In light of these, you can better appreciate the nuances between the
functions <code>quote</code> and <code>function</code>, as pertains to symbols. When you
write <code>(quote some-symbol)</code> this is the same as <code>'some-symbol</code>, which
is then expected to be evaluated by <code>symbol-value</code>. Whereas <code>(function
some-symbol)</code> is the way to state <code>#'some-symbol</code> and to suggest that
<code>symbol-function</code> is how to get the data out of the symbol in
question.
</p>

<p>
While these technicalities matter, you do not need to be an expert in
them to write decent code. What you need to remember when working with
Elisp is that you can tell from the context alone whether something is
treated as a function call or as a variable.
</p>
</div>
</div>
<div id="outline-container-h:buffers-as-data-structures" class="outline-2">
<h2 id="h:buffers-as-data-structures"><span class="section-number-2">7.</span> Buffers as data</h2>
<div class="outline-text-2" id="text-h:buffers-as-data-structures">
<p>
In its simplest form, a buffer holds data as a sequence of characters.
When you open a file, for example, Emacs reads the file contents from
the disk, loads them into its running memory, and displays the
contents in a buffer. This buffer now has as its data the contents of
the file and anything else that can be extracted therefrom. Each
character exists at a given position, which is a number. The function
<code>point</code> gives you the numeric position at the point you are on, which
typically corresponds to where the cursor is (<a href="#h:evaluate-emacs-lisp">Evaluate Emacs Lisp</a>).
</p>

<p>
At the beginning of a buffer, <code>point</code> returns the value of <code>1</code> (<a href="#h:side-effect-and-return-value">Side
effect and return value</a>). There are plenty of functions that return a
buffer position, such as <code>point-min</code>, <code>point-max</code>, <code>re-search-forward</code>,
and <code>line-beginning-position</code>. Some of those will have side effects,
like <code>re-search-forward</code> which moves the cursor to the given match.
</p>

<p>
When you program in Emacs Lisp, you frequently rely on buffers to do
some of the following:
</p>

<dl class="org-dl">
<dt>Extract file contents as a string</dt><dd>Think of the buffer as a large
string. You can get the entirety of its contents as one potentially
massive string by using the function <code>buffer-string</code>. You may also
get a substring between two buffer positions, such as with the
<code>buffer-substring</code> function or its <code>buffer-substring-no-properties</code>
counterpart (<a href="#h:text-has-its-own-properties">Text has its own properties</a>). Imagine you do this as
part of a wider operation that (i) opens a file, (ii) goes to a
certain position, (iii) copies the text between some boundaries,
(iv) switches to another buffer, and (v) writes what it found to
this new buffer.</dd>
</dl>

<dl class="org-dl">
<dt>Present the results of some operation</dt><dd>You may have a function
that shows upcoming holidays. Your code does the computations behind
the scenes and ultimately writes some text to a buffer. The end
product is on display. Depending on how you go about it, you will
want to evaluate the function <code>get-buffer-create</code> or its more strict
<code>get-buffer</code> alternative. If you need to clear the contents of an
existing buffer, you might use the <code>with-current-buffer</code> macro to
temporarily switch to the buffer you are targetting and then either
call the function <code>erase-buffer</code> to delete everything or limit the
deletion to the range betweeen two buffer positions with
<code>delete-region</code>. Finally, the functions <code>display-buffer</code> or
<code>pop-to-buffer</code> will place the buffer in an Emacs window. When you
present data, you may also choose to have some specialised major
mode that sets the appropriate settings (<a href="#h:what-are-major-and-minor-modes">What are major and minor modes</a>).</dd>
</dl>

<dl class="org-dl">
<dt>Associate variables with a given buffer</dt><dd>In Emacs Lisp, variables
can take a buffer-local value which differs from its global
counterpart. Some variables are even declared to always be
buffer-local, such as the <code>buffer-file-name</code>, <code>fill-column</code>, and
<code>default-directory</code>. Suppose you are doing something like returning
a list of buffers that visit files in a given directory. You would
iterate through the return value of the <code>buffer-list</code> function to
filter the results accordingly by testing for a certain value of
<code>buffer-file-name</code> (<a href="#h:basic-control-flow-with-if-cond-and-others">Basic control flow with <code>if</code>, <code>cond</code>, and others</a>).
The special form <code>setq-local</code> sets the buffer-local value of a
variable (<a href="#h:symbols-balanced-expressions-and-quoting">Symbols, balanced expressions, and quoting</a>).</dd>
</dl>

<p>
The latter point is perhaps the most open-ended one. Buffers are like
a bundle of variables. This bundle includes their contents, any
overlays or text properties associated with the contents, the active
major mode, any active minor modes, and all the buffer-local values of
other variables (<a href="#h:what-are-major-and-minor-modes">What are major and minor modes</a>). In the following
code block, I am using the <code>seq-filter</code> function to iterate through
the return value of the function <code>buffer-list</code> to get a list of
buffers that (i) are not hidden from the user&rsquo;s view and (ii) have
<code>text-mode</code> as their major mode or a derivative thereof (<a href="#h:mapping-through-a-list-of-elements">Mapping over a list of elements</a>).
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">(seq-filter
 (lambda (buffer)
   "Return BUFFER if it is visible and its major mode derives from `text-mode'."
   (with-current-buffer buffer
     ;; The convention for buffers which are not meant to be seen by
     ;; the user is to start their name with an empty space.  We are
     ;; not interested in those right now.
     (and (not (string-prefix-p " " (buffer-name buffer)))
          (derived-mode-p 'text-mode))))
 (buffer-list))
</pre>
</div>

<p>
This will return a list of buffer objects or <code>nil</code>. The above relies
on a <code>lambda</code> to do its work. This <code>lambda</code> exists only inside the
given function call. If we ever need to perform the same check, it is
better to give a name to the <code>lambda</code>, i.e. to define a function. As
such, we may write the following (<a href="#h:when-to-use-a-named-function-or-a-lambda-function">When to use a named function or a lambda function</a>):
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">(defun my-buffer-visble-and-text-p (buffer)
  "Return BUFFER if it is visible and its major mode derives from `text-mode'."
  (with-current-buffer buffer
    ;; The convention for buffers which are not meant to be seen by
    ;; the user is to start their name with an empty space.  We are
    ;; not interested in those right now.
    (and (not (string-prefix-p " " (buffer-name buffer)))
         (derived-mode-p 'text-mode))))

(seq-filter #'my-buffer-visble-and-text-p (buffer-list))
</pre>
</div>

<p>
As with buffers, Emacs windows and frames have their own parameters. I
will not cover those as their utility is more specialised and the
concepts are the same. Just know that they are data objects that you
may use in your programs.
</p>
</div>
</div>
<div id="outline-container-h:text-has-its-own-properties" class="outline-2">
<h2 id="h:text-has-its-own-properties"><span class="section-number-2">8.</span> Text has its own properties</h2>
<div class="outline-text-2" id="text-h:text-has-its-own-properties">
<p>
Just as with buffers that work like data objects (<a href="#h:buffers-as-data-structures">Buffers as data</a>),
any text may also have properties associated with it&#x2014;and symbols
too, by the way (<a href="#h:add-metadata-to-symbols">Add metadata to symbols</a>). This is metadata that you
inspect using Emacs Lisp functions. For example, when you have syntax
highlighting in some programming buffer, it is the effect of
underlying text properties. Some function takes care to &ldquo;propertise&rdquo;
or to &ldquo;fontify&rdquo; the relevant text. It applies to it an object known as
a &ldquo;face&rdquo;.
</p>

<p>
Faces are constructs of the Emacs display engine that bundle together
typography and colour attributes, such as the font family and weight,
as well as foreground and background hues. Every piece of text uses
the <code>default</code> face at minimum. Whenever a colour is present or some
other typographic style, then there are more faces present.
</p>

<p>
To get a <code>*Help*</code> buffer with information about the text properties
where the cursor is, type M-x (<code>execute-extended-command</code>)
and then invoke the command <code>describe-char</code>. It will tell you about
the character it identifies, what font it is rendered in, which code
point it is, and what its text properties are, if any. If there is a
face, that <code>*Help*</code> buffer will link to it. Following the link shows
you what the face&rsquo;s effective attributes are.
</p>

<p>
Suppose you are writing your own major mode. At the early stage of
experimentation, you want to manually add text properties to all
instances of the phrase <code>I have properties</code> in a buffer whose major
mode is <code>fundamental-mode</code>, so you produce a function that adds text
properties where relevant, like the following (<a href="#h:the-match-data-of-the-last-search">The match data of the last search</a>):
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">(defun my-add-properties ()
  "Add properties to the text \"I have properties\" across the current buffer."
  (goto-char (point-min))
  (while (re-search-forward "I have properties" nil t)
    (add-text-properties (match-beginning 0) (match-end 0) '(face error))))
</pre>
</div>

<p>
Actually test this. Use C-x b (<code>switch-to-buffer</code>), type in
some random characters that do not match an existing buffer, and then
press RET to visit that new buffer. It runs <code>fundamental-mode</code>,
meaning that no special &ldquo;fontification&rdquo; is in effect and, thus,
<code>my-add-properties</code> will work as intented (<a href="#h:what-are-major-and-minor-modes">What are major and minor modes</a>).
Now paste this snippet of text:
</p>

<div class="org-src-container">
<pre class="src src-fundamental">This is some sample text. Will the phrase "I have properties" use the `error' face?

What does it even mean for I have properties to be rendered as `error'?
</pre>
</div>

<p>
Continue with M-: (<code>eval-expression</code>) and call the function
<code>my-add-properties</code>, i.e. <code>(my-add-properties)</code>. Did it work? The face
this function is applying is called <code>error</code>. Ignore the semantics of
that word: I picked <code>error</code> simply because it typically is styled in a
fairly intense and obvious way (though your current theme may do
things differently).
</p>

<p>
There are functions which find the properties at a given buffer
position and others which can search forward and backward for a
certain property. The specifics do not matter right now. All I want
you to remember is that the text may hold data beyond its constituent
characters. For more details, type M-x (<code>execute-extended-command</code>)
to call the command <code>shortdoc</code>. It will ask you for a documentation
group. Pick <code>text-properties</code> to learn more. Well, actually use
<code>shortdoc</code> for everything listed there&#x2014;I do it frequently!
</p>

<p>
The example above with <code>add-text-properties</code> is not how major modes
typically do things (<a href="#h:what-are-major-and-minor-modes">What are major and minor modes</a>). Emacs has a more
sophisticated mechanism, called &ldquo;font-lock&rdquo;. It is responsible for
fontifying the buffer. A major mode will thus set up the
<code>font-lock-defaults</code> variable (minor modes will typically modify the
<code>font-lock-keywords</code>). How exactly this works is not important for our
purposes. The general idea is that for each style, say level 1
headings in Org, you either write a regular expression or, better, a
function that does pattern matching. You then specify which face
corresponds to the matched pattern or subgroups thereof and how should
the faces be applied in case of competing fontification rules.
</p>

<p>
What ultimately matters is that we have buffers as data objects and
text that has metadata. We use this to our advantage to parse the text
available to us.
</p>
</div>
</div>
<div id="outline-container-h:add-metadata-to-symbols" class="outline-2">
<h2 id="h:add-metadata-to-symbols"><span class="section-number-2">9.</span> Add metadata to symbols</h2>
<div class="outline-text-2" id="text-h:add-metadata-to-symbols">
<p>
A symbol can be a reference to itself or to some value (<a href="#h:basics-of-how-lisp-works">Basics of how Lisp works</a>).
For variables, <code>nil</code> returns <code>nil</code>, <code>t</code> returns <code>t</code>, and <code>my-name</code>
will return <code>Protesilaos</code> if it is defined thus. For functions, the
symbol actually is an alias for a given computation, i.e. a <code>lambda</code>
expression whose evaluation may produce side effects and return a
value (<a href="#h:symbols-balanced-expressions-and-quoting">Symbols, balanced expressions, and quoting</a>).
We can get the value of a variable either by evaluating it without a
quote or by passing its symbol as an argument to the function
<code>symbol-value</code> (<a href="#h:side-effect-and-return-value">Side effect and return value</a>). Consider these:
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">;; Define the variable `my-name' and give it a value.
(defvar my-name "Protesilaos")
;; =&gt; my-name

;; What is the value of the symbol `my-name', where the symbol is a variable?
(symbol-value 'my-name)
;; =&gt; "Protesilaos"

;; What is the value of the variable `my-name' if I do not quote it?
my-name
;; =&gt; "Protesilaos"
</pre>
</div>

<p>
Similarly, we can get the function value of a symbol by calling <code>symbol-function</code>:
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">(defun my-hello (person)
  "Say hello to PERSON."
  (message "Hello %s" person))
;; =&gt; my-hello

;; What is the symbol `my-hello' as a function?
(symbol-function 'my-hello)
;; =&gt; #[(person) ((message "Hello %s" person)) nil nil "Say hello to PERSON."]

;; What happens if I call `my-hello' without parentheses and/or a quote?
my-hello
;; =&gt; ERROR
</pre>
</div>

<p>
What we know up until now about symbols is that they can be associated
with a data point, be it as variables or functions. That is their
value. They may also have metadata, which in Emacs Lisp terminology is
known as a &ldquo;property&rdquo; or &ldquo;symbol properties&rdquo;. In its simplest form, a
symbol <code>my-name</code> is associated with arbitrary metadata through the use
of the <code>put</code> function:
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">(put 'my-name 'my-nicknames '("Prot" "The dog" "The Cypriot" "The Greek-mainlander"))
;; =&gt; ("Prot" "The dog" "The Cypriot" "The Greek-mainlander")
</pre>
</div>

<p>
Similarly, the <code>get</code> function returns the value of the given symbol&rsquo;s
property.
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">(get 'my-name 'my-nicknames)
;; =&gt; ("Prot" "The dog" "The Cypriot" "The Greek-mainlander")
</pre>
</div>

<p>
In the above two examples, the symbol we are operating on is <code>my-name</code>
and its property is <code>my-nicknames</code>. The value given to the property
can be any Elisp object, such as a number, a string, or a list. In the
above examples we have a list of strings. To retrieve all the
properties of a symbol, we use the function <code>symbol-plist</code>, where
&ldquo;plist&rdquo; stands for &ldquo;property list&rdquo;.
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">(symbol-plist 'my-name)
;; =&gt; (variable-documentation "This is some documentation about my name." my-nicknames ("Prot" "The dog" "The Cypriot" "The Greek-mainlander"))
</pre>
</div>

<p>
What <code>symbol-plist</code> returns is an even-numbered list where each odd
number is the key and each even number is its corresponding value.
This is the plist. Looking at what we have above, we can use
<code>plist-get</code> to extract a value from the property list, thus:
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">(plist-get (symbol-plist 'my-name) 'my-nicknames)
;; =&gt; ("Prot" "The dog" "The Cypriot" "The Greek-mainlander")
</pre>
</div>

<p>
Though in this specific case we already have <code>get</code> do the same work,
so <code>plist-get</code> is superfluous&#x2014;it is applicable in other contexts
where you need to work with a plist.
</p>

<p>
The <code>my-name</code> is the symbol of a variable, though the symbol of a
function will work the same way. Let me show you how the <code>my-hello</code> we
defined further above will get its <code>cool-factor</code>:
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">(put 'my-hello 'cool-factor t)
</pre>
</div>

<p>
You <code>get</code> how this works. In working code, you will probably not need
to use properties in this way. Some macro will handle them internally.
Consider this real-world example from the Emacs source code where the
function <code>string-chop-newline</code> is declared with concomitant metadata
(the relevant symbols for <code>declare</code>, such as <code>pure</code> and
<code>side-effect-free</code>, are documented in the Emacs Lisp Reference
Manual):
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">(defun string-chop-newline (string)
  "Remove the final newline (if any) from STRING."
  (declare (pure t) (side-effect-free t))
  (string-remove-suffix "\n" string))
</pre>
</div>

<p>
In practice, this boils down to a <code>put</code> operation, as illustrated
above. You just do not need to specify it yourself. Same principle for
variables. For example, the Lisp macro <code>defcustom</code> defines a variable
with some special metadata which renders it as a so-called &ldquo;user
option&rdquo;: it is a variable that the programmer has designed for users
to modify and, therefore, is meant to have a predictable,
well-supported behaviour. Internally, <code>defcustom</code> will add properties
to associate the variable with its original value and to do everything
necessary to render concrete its particularities, such as whether the
variable is safe as a buffer-local value (<a href="#h:buffers-as-data-structures">Buffers as data</a>).
</p>

<p>
Chances are that you will not need to deal with symbol properties,
especially not while you are getting started. Though know that there
are Emacs Lisp programs that make extensive use of them. If your
program needs to work with symbol properties, you have already gotten
enough exposure to them to proceed with confidence. May you make the
most out of this occasion!
</p>
</div>
</div>
<div id="outline-container-h:what-are-major-and-minor-modes" class="outline-2">
<h2 id="h:what-are-major-and-minor-modes"><span class="section-number-2">10.</span> What are major and minor modes</h2>
<div class="outline-text-2" id="text-h:what-are-major-and-minor-modes">
<p>
Buffers are data objects that can be associated with more data
(<a href="#h:buffers-as-data-structures">Buffers as data</a>). A common use-case is to have buffer-local variables
that control how certain functions behave when called with the given
buffer as current. If the text is read-only, for example, this is due
to a buffer-local variable that inhibits the ordinary behaviour of
text editing commands. When a certain key binding does something
different in one buffer than in another, this too is because of
buffer-local variables. Or, to be more precise, the buffer applies its
own local keymap which overrides the global keymap.
</p>

<p>
In essence, major and minor modes are commands which activate settings
once they are invoked, such as with M-x
(<code>execute-extended-command</code>) (minor mode commands have a toggle
behaviour, but more on that below). If you look at the definition of a
major mode, for instance, you will notice that all it seems to do is
specify buffer-local values for certain variables. Those typically
pertain to standardised definitions, such as what is a paragraph, what
constitutes a comment, how should indentation be handled, whether
there is an outline or some kind or index with points of interest in
the buffer, and the fontification settings or other text properties
(<a href="#h:text-has-its-own-properties">Text has its own properties</a>). Here is a simple example from the
source code of Emacs, specifically text.el:
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">(define-derived-mode text-mode nil "Text"
  "Major mode for editing text written for humans to read.
In this mode, paragraphs are delimited only by blank or white lines.
You can thus get the full benefit of adaptive filling
 (see the variable `adaptive-fill-mode').
\\{text-mode-map}
Turning on Text mode runs the normal hook `text-mode-hook'."
  (setq-local text-mode-variant t)
  (setq-local require-final-newline mode-require-final-newline)

  ;; Enable text conversion in this buffer.
  (setq-local text-conversion-style t)
  (add-hook 'context-menu-functions 'text-mode-context-menu 10 t)
  (when (eq text-mode-ispell-word-completion 'completion-at-point)
    (add-hook 'completion-at-point-functions #'ispell-completion-at-point 10 t)))
</pre>
</div>

<p>
The docstring of <code>text-mode</code> has some markup that controls what the
<code>*Help*</code> buffer will show (<a href="#h:introspecting-emacs">Introspecting Emacs</a>). The combination of
backtick and single quote makes <code>*Help*</code> treat the given text as a
symbol that it links to. Thus, by reading about <code>text-mode</code>, we also
get a link to <code>adaptive-fill-mode</code>. Following that link yields the
relevant documentation. The braces or curly brackets, the opening of
which is escaped with backslashes, is a placeholder to get the
contents of a keymap listed there. What we will then get in the
<code>*Help*</code> buffer is a listing with all the keys and the commands they
are bound to. Beside these technicalities, we have some <code>setq-local</code>
and <code>add-hook</code> calls that also have a local scope (<a href="#h:hooks-and-the-advice-mechanism">Hooks and the advice mechanism</a>).
</p>

<p>
What you learn from studying the definition of <code>text-mode</code> is that it
effectively is a bundle of settings. By setting a buffer to use the
major mode <code>text-mode</code>, we are practically opting in to those
settings. If you create a new buffer, whose major mode is
<code>fundamental-mode</code> by default, you can manually set all of the above
without explicitly calling <code>text-mode</code>. Doing so will give you
most&#x2014;though not all!&#x2014;of what <code>text-mode</code> does. Actually, do not
try this at home: nobody has time for such cumbersome labour and it
anyway is not doing what you actually need.
</p>

<p>
If you do proceed though, know that you are not getting the full
effect of <code>text-mode</code>. Internally, Emacs takes care of a few more
things when you invoke the command of the major mode. Specifically, it
updates the variable <code>major-mode</code> to point to the symbol of the
currently active mode. It also specifies the parent major mode, if
any, from where the current major mode inherits its settings.
Furthermore, Emacs updates the local keymap and also calls the hook of
the given major mode. And if you are really curious, it even changes
the applicable syntax table and table of abbreviations. The syntax
table determines such things as what is a symbol and what is
punctuation, while the abbreviations&rsquo; table, known as &ldquo;abbrev table&rdquo;,
holds data that is used by the built-in minor mode called
<code>abbrev-mode</code>.
</p>

<p>
Where do all these come from? Well, <code>define-derived-mode</code> is a macro
that instantiates everything using the name of the mode as a prefix
(<a href="#h:evaluation-inside-of-a-macro-or-special-form">Evaluation inside of a macro or special form</a>). If, for example, there
is a major mode called <code>football-mode</code>, in reference to the game where
players kick a ball around the pitch instead of hugging it in
confusion, then there will be a <code>football-mode-hook</code>,
<code>football-mode-map</code>, <code>football-mode-syntax-table</code>, and
<code>football-mode-abbrev-table</code>.
</p>

<p>
Major modes are mutually exclusive. There can only be one per buffer,
though there are clever ways to have the effects of many major modes
in one buffer, like what Org mode does with source code blocks that
retain some of the qualities of the programming modes they reference.
The reason a buffer has one major mode is due to the conflicts that
would otherwise arise. For example, Emacs would have an unpredictable
behaviour if one major mode defined, say, dashes as punctuation while
another treated them as constituent characters of symbols. A
programmer could come up with increasingly complex heuristics, though
there is a certain elegance to the &ldquo;one buffer, one major mode&rdquo;
approach. In some contexts, like in modern web development, the major
mode effectively is a superset of other major modes, such that it can
work with a mixture of JavaScript, HTML, and CSS.
</p>

<p>
By contrast, there can be more than one minor mode per buffer.
Sometimes the functionality of one will directly contradict another,
though they generally combine harmoniously. For example, Emacs comes
with lots of useful minor modes out-of-the-box. One of them highlights
the current line. It is called <code>hl-line-mode</code>. Another displays line
numbers on the side of the window: its symbol is <code>display-line-numbers-mode</code>.
Those can work together in a buffer in conjunction with other minor
modes, such as <code>show-paren-mode</code>, that highlights matching
parentheses, and <code>electric-pair-mode</code> that inserts certain characters
as pairs, like double quotes and parentheses. Because minor modes
usually have complementary functionality, they are designed to work
both locally and globally. Whatever conflicts are up to the user to
resolve.
</p>

<p>
Otherwise, minor modes follow the rationale of major modes: they are
commands that typically set some variables (not to imply they are
simple though, as they can do anything they want). Since minor modes
can, in principle, coexist, calling their command with M-x
(<code>execute-extended-command</code>) toggles them on and off. Such a switch
would not be of much use for major modes: it would be the same as
activating <code>fundamental-mode</code>.
</p>

<p>
Once you delve deeper, minor modes are also supposed to have a
different scope of application. A minor mode should not be messing
around with the syntax table, for example, as then there could be a
minor mode that breaks the current major mode. This could be done
though, but the programmer must be extra careful how they go about
dealing with the resulting complexity, given that other minor modes
may also be in effect while major modes may be doing something of
their own that has to be accounted for. The macro which defines a
minor mode, namely, <code>define-minor-mode</code> will also not do exactly what
<code>define-derived-mode</code> does for major modes. It will not specify a
parent mode as there is no such concept of inheritance for minor
modes. There will also be no abbrev table, no syntax table, and no
keymap. A keymap can be associated with a minor mode, though it must
be done explicitly with a special keyword. The documentation of
<code>define-minor-mode</code> covers the details (<a href="#h:introspecting-emacs">Introspecting Emacs</a>).
</p>

<p>
In general, you will benefit from thinking of Emacs as one big mutable
state. Within it there are compartments with their own peculiarities.
Emacs is designed to give priority to the particular over the general.
The specific/local overrides the generic/global. This is true for
variables, keymaps, abbrev tables, faces (via face remapping that we
do not need to cover), and, in an analogous way, is also true for
<code>let</code> bindings where the innermost overrides the outer ones
(<a href="#h:evaluation-inside-of-a-macro-or-special-form">Evaluation inside of a macro or special form</a>). For Emacs&rsquo; modes, a
major mode will override the settings of the major mode it is derived
from, while a minor mode will take precedence over a major mode. Be
mindful of this so that you can use it to your advantage.
</p>
</div>
</div>
<div id="outline-container-h:hooks-and-the-advice-mechanism" class="outline-2">
<h2 id="h:hooks-and-the-advice-mechanism"><span class="section-number-2">11.</span> Hooks and the advice mechanism</h2>
<div class="outline-text-2" id="text-h:hooks-and-the-advice-mechanism">
<p>
You know that each major and minor mode runs its own hook (<a href="#h:what-are-major-and-minor-modes">What are major and minor modes</a>).
A hook is a variable that holds a list of functions. These are
evaluated in sequence when the hook runs. The functions are normally
called without an argument, so they must have their parameters set up
correctly to avoid the error with the mismatching number of mandatory
arguments. Some hooks are considered &ldquo;abnormal&rdquo; in that they call
their functions with one or more arguments. How many arguments is up
to each hook. I will show you in practice.
</p>

<p>
The function <code>add-hook</code> adds a function to a hook. It looks like the
function call in Emacs Lisp (<a href="#h:basics-of-how-lisp-works">Basics of how Lisp works</a>):
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">(add-hook 'text-mode-hook #'visual-line-mode)
</pre>
</div>

<p>
The <code>text-mode-hook</code> is exactly what its name suggests: the hook that
<code>text-mode</code> runs. It is evaluated after the <code>text-mode</code> is done
setting all its variables. Generally, it is common for hooks to run at
the end of the function that calls them, though it is also possible to
run a hook before or during some operation. Notable examples are
<code>before-save-hook</code> and <code>pre-command-hook</code>. The rule of thumb is this:
if the hook&rsquo;s symbol does not imply otherwise, the hook is run at the
end.
</p>

<p>
In the above example, you noticed how I added a quote to the variable
<code>text-mode-hook</code> and a sharp quote to the function <code>visual-line-mode</code>
(<a href="#h:symbols-balanced-expressions-and-quoting">Symbols, balanced expressions, and quoting</a>). The expression would
have still worked fine with a regular quote instead of the sharp
quote. Though these added semantics can help us catch problems during
byte compilation when we are developing packages.
</p>

<p>
The <code>add-hook</code> has a global effect by default: it updates the global
value of the variable, not its buffer-local value (<a href="#h:buffers-as-data-structures">Buffers as data</a>).
For example, if you want to do something with, say,
<code>hl-line-mode-hook</code> only in a specific file, then you need to arrange
for the <code>add-hook</code> side effect to be applied in the buffer that visits
the given file (<a href="#h:side-effect-and-return-value">Side effect and return value</a>). This is done by passing
a non-<code>nil</code> argument to the <code>LOCAL</code> parameter of <code>add-hook</code>, as we
learn from its documentation (<a href="#h:introspecting-emacs">Introspecting Emacs</a>):
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">(defun my-display-line-numbers-toggle ()
  "Enable `display-line-numbers-mode' if `hl-line-mode' is active.
Else disable `display-line-numbers-mode'."
  (if hl-line-mode
      (display-line-numbers-mode 1)
    (display-line-numbers-mode -1)))

(add-hook 'hl-line-mode-hook #'my-display-line-numbers-toggle nil t)
</pre>
</div>

<p>
The function <code>my-display-line-numbers-toggle</code> is now set up to run in
the current buffer whenever <code>hl-line-mode</code> is enabled or disabled. Try
it! Here you will also notice a technicality. Each minor mode has a
function and a variable of the same symbol. When the minor mode is
enabled, the variable is set to a non-<code>nil</code> value.
</p>

<p>
Internally, a function that runs a hook does it with a call to <code>run-hooks</code>:
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">(run-hooks 'text-mode-hook)
</pre>
</div>

<p>
The <code>run-hooks</code> is plural because it has a <code>&amp;rest</code> keyword in its
parameters, meaning that we can give it as many arguments as we want
and it will collect them all into a single list that will then be
handled appropriately. Thus:
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">;; No matter how many arguments we pass, they will be collected into a
;; single HOOKS list, owning to the &amp;rest keyword.
(run-hooks 'text-mode-hook 'hl-line-mode-hook)
</pre>
</div>

<p>
To remove a function from a hook variable, we use <code>remove-hook</code>. It is
similar to <code>add-hook</code>, though check its documentation because its
parameters are not the same:
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">;; Remove globally
(remove-hook 'hl-line-mode-hook #'my-display-line-numbers-toggle)

;; Remove locally
(remove-hook 'hl-line-mode-hook #'my-display-line-numbers-toggle t)
</pre>
</div>

<p>
Thus far we have examined &ldquo;normal hooks&rdquo;, i.e. hooks whose functions
are called without any arguments. By convention, a normal hook has the
<code>-hook</code> suffix. &ldquo;Abnormal hooks&rdquo; are called with one or more
arguments. Each hook decides how many those are and what they are
expected to be. Abnormal hooks have the suffix <code>-functions</code>. The
<code>add-hook</code> and <code>remove-hook</code> work the same way for abnormal hooks.
What is different is the functions we have to add to them, in order to
get the expected results: they must be able to handle the arguments
passed to them. Here is an example of defining and then calling a
hook. Remember that hooks are ordinary variables:
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">;; Example with a normal hook:
(defvar my-normal-hook nil
  "Normal hook for `my-hello-world'.")

(defun my-hello-world ()
  "Print a hello message and run `my-normal-hook'."
  (message "Hello world")
  (run-hooks 'my-normal-hook))

;; Example with an abnormal hook:
(defvar my-normal-functions nil
  "List of functions to call after `my-say-hello'.
Each function takes two arguments, the name of the person and their
country of residence.")

(defun my-say-hello (person country)
  "Say hello to PERSON from COUNTRY."
  (message "Hello %s from %s" person country)
  (run-hook-with-args 'my-normal-functions person country))
</pre>
</div>

<p>
The <code>run-hook-with-args</code> takes one hook and the arguments that are
passed to its functions. Otherwise the functionality is the same
between running a normal hook and an abnormal hook.
</p>

<p>
The logic of a hook is simple &ldquo;let the user do something before or
after some event&rdquo;. The only constraint is that the programmer has
provided such a point of entry. There are times though where you want
to do something, but there is no hook for it. This is where the advice
mechanism comes in. We can &ldquo;advise&rdquo; a function to run some code before
or after or even around some other function. In practice, we can
redefine the original function by wrapping it in our function. The
documentation string of <code>add-function</code> provides a concise overview,
but let me show you a couple of common use-cases. Before you proceed,
promise me to use the advice mechanism with care, because you can
produce unexpected results and create bugs in other packages.
</p>

<p>
Probably the simplest use of the advice mechanism is to do something
after a given function is called. Conceptually, this is like a hook in
the ordinary scenario. You do it with the function <code>advice-add</code>:
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">(defun my-after-next-line (&amp;rest _)
  "Message the current line number while ignoring any arguments."
  (message "Current line: %s" (line-number-at-pos)))

(advice-add #'next-line :after #'my-after-next-line)
</pre>
</div>

<p>
Once you evaluate this, move down a line and notice how you get the
message with the current line number. Now do <code>advice-remove</code> to undo
the effect:
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">(advice-remove #'next-line #'my-after-next-line)
</pre>
</div>

<p>
Go back to the definition of <code>my-after-next-line</code>. It says that it
ignores any arguments. This is done by writing the <code>&amp;rest</code> keyword and
then the name of some parameter prefixed by an underscore. If we do
not care about the name either, we just write the underscore. The
underscore is how we convey the meaning of &ldquo;I shall ignore this, mate;
cheers!&rdquo;. The reason I wrote it this way is because the advice is
called with the arguments of the original function. If I wanted to do
something with one or more of them, I could have written the list of
parameters in one of the following ways:
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">;; Two mandatory parameters.
(defun my-demo (parameter-one parameter-two)
  ...)

;; A mandatory parameter and a list of zero or more other acceptable arguments.
(defun my-demo (parameter-one &amp;rest all-others)
  ...)

;; As above but we declare the intention to ignore ALL-OTHERS.
(defun my-demo (parameter-one &amp;rest _all-others)
  ...)

;; Two optional parameters, meaning that the function can be called
;; without them.  But if the function is called with arguments, those
;; must be maximum two.
(defun my-demo (&amp;optional parameter-one parameter-two)
  ...)

;; One mandatory and one optional parameter.
(defun my-demo (parameter-one &amp;optional parameter-two)
  ...)

;; One optional parameter and a list of zero or more acceptable arguments.
(defun my-demo (&amp;optional parameter-one &amp;rest all-others)
  ...)

;; As above, but ALL-OTHERS is meant to be ignored.
(defun my-demo (&amp;optional parameter-one &amp;rest _all-others)
  ...)

;; One mandatory, one optional parameter, and then a list of zero or
;; more acceptable arguments.
(defun my-demo (parameter-one &amp;optional parameter-two &amp;rest all-others)
  ...)
</pre>
</div>

<p>
Keep the above in mind while adding an advice. If you get the
parameters wrong, then errors will ensue. The easiest is <code>(&amp;rest _)</code>
though it may not be what you need to do.
</p>

<p>
Other than the <code>:after</code> advice, the <code>:around</code> is common. You want to
use it to do something before and/or after the original function is
called. For example, I want the <code>fundamental-mode</code> to actually have a
hook, so I do this, which also happens to tie together what we have
covered in this chapter:
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">(defvar my-fundamental-mode-hook nil
  "Normal hook for `fundamental-mode' (which is missing by default).")

(defun my-fundamental-mode-run-hook (&amp;rest args)
  "Apply ARGS and then run `my-fundamental-mode-hook'."
  (apply args)
  (run-hooks 'my-fundamental-mode-hook))

(advice-add #'fundamental-mode :around #'my-fundamental-mode-run-hook)
</pre>
</div>

<p>
After installing this advice, calling <code>fundamental-mode</code> will also run
the <code>my-fundamental-mode-run-hook</code>. Neat!
</p>

<p>
Check how the function <code>my-fundamental-mode-run-hook</code> makes use of the
<code>args</code> it gets. The <code>(apply args)</code> effectively means &ldquo;call the
original function with its original arguments&rdquo;. It works because the
function and its arguments are all part of one list, namely <code>args</code>,
where the symbol of the function is the first element and the
arguments to it are the remaining elements&#x2014;exactly how a function is
called. Now, it so happens that <code>fundamental-mode</code> is called without
any arguments, but the code will still work even if <code>fundamental-mode</code>
is rewritten to have parameters. What matters is that the pattern of
<code>&amp;rest args</code> and then <code>(apply args)</code> is robust.
</p>

<p>
I like to think of <code>apply</code> as (i) &ldquo;here is a quoted list, remove the
quote and evaluate it&rdquo; or (ii) &ldquo;here is the symbol of a function and
then a quoted list of arguments, so append the symbol to the following
list, remove the quote, and evaluate it&rdquo;. In other words, these two
examples will yield the same results (<a href="#h:symbols-balanced-expressions-and-quoting">Symbols, balanced expressions, and quoting</a>):
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">(apply '(+ 1 1 1))
;; =&gt; 3

(apply #'+ '(1 1 1))
;; =&gt; 3
</pre>
</div>

<p>
Because the function <code>my-fundamental-mode-run-hook</code> I was showing you
earlier has full control over when to make use of the <code>args</code>, it gives
me the power to do something before I call <code>apply</code>. I do not need it
here, but you get the idea.
</p>

<p>
The advice mechanism is powerful, though you have to use it
judiciously. Emacs gives you the power. It is then up to you to act
responsibly. Otherwise, you will encounter nasty bugs and create
problems for others.
</p>
</div>
</div>
<div id="outline-container-h:partial-evaluation-inside-of-a-list" class="outline-2">
<h2 id="h:partial-evaluation-inside-of-a-list"><span class="section-number-2">12.</span> Evaluate some elements inside of a list</h2>
<div class="outline-text-2" id="text-h:partial-evaluation-inside-of-a-list">
<p>
You already have an idea of how Emacs Lisp code looks like (<a href="#h:symbols-balanced-expressions-and-quoting">Symbols, balanced expressions, and quoting</a>).
You have a list that is either evaluated or taken as-is (<a href="#h:basics-of-how-lisp-works">Basics of how
Lisp works</a>). There is another case where a list should be selectively
evaluated or, more specifically, where it should be treated as data
instead of a function call with some elements inside of it still
subject to evaluation.
</p>

<p>
In the following code block, I am defining a variable called
<code>my-greeting-in-greek</code>, which is a common phrase in Greek that
literally means &ldquo;health to you&rdquo; and is pronounced as &ldquo;yah sou&rdquo;. Why
Greek? Well, you got the <code>lambda</code> that engendered this whole business
with Lisp, so you might as well get all the rest (<a href="#h:when-to-use-a-named-function-or-a-lambda-function">When to use a named function or a lambda function</a>)!
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">(defvar my-greeting-in-greek "Î“ÎµÎ¹Î± ÏƒÎ¿Ï…"
  "Basic greeting in Greek to wish health to somebody.")
</pre>
</div>

<p>
Now I want to experiment with the <code>message</code> function to better
understand how evaluation works. Let me start with the scenario of
quoting the list, thus taking it as-is:
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">(message "%S" '(one two my-greeting-in-greek four))
;;=&gt; "(one two my-greeting-in-greek four)"
</pre>
</div>

<p>
The <code>message</code> function has the side effect of appending the message it
produces to the <code>*Messages*</code> buffer (<a href="#h:side-effect-and-return-value">Side effect and return value</a>).
You can review the log this way. Now check the code we have here. You
will notice that the variable <code>my-greeting-in-greek</code> is not evaluated.
I get the symbol, the actual <code>my-greeting-in-greek</code>, but not the value
it represents. This is the expected result, because the entire list is
quoted and, ipso facto, everything inside of it is not evaluated. Now
pay attention to the next code block to understand how I can tell
Emacs that I want the entire list to still be quoted but for the
singular element <code>my-greeting-in-greek</code> to be evaluated. This means
that <code>my-greeting-in-greek</code> is replaced by its value while everything
else is taken as-is:
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">(message "%S" `(one two ,my-greeting-in-greek four))
;; =&gt; "(one two \"Î“ÎµÎ¹Î± ÏƒÎ¿Ï…\" four)"
</pre>
</div>

<p>
The syntax here is special. Instead of a single quote, I am using the
backtick character or back quote or grave accent, to write an
expression that is &ldquo;quasi quoted&rdquo;. The backtick behaves like the
single quote except for anything that is preceded by a comma. The
comma only has meaning inside of a quasi quoted list, otherwise you
get an error. The comma is an instruction to &ldquo;evaluate the thing that
follows&rdquo;. It is written as a prefix without a space after it and
applies to the thing it is attached to. The &ldquo;thing&rdquo; that follows is
either a symbol or a list. The list can, of course, be a function call
of arbitrary complexity. Let me then use <code>concat</code> to greet a certain
person all while returning everything as a list:
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">(message "%S" `(one two ,(concat my-greeting-in-greek " " "Î ÏÏ‰Ï„ÎµÏƒÎ¯Î»Î±Îµ") four))
;; =&gt; "(one two \"Î“ÎµÎ¹Î± ÏƒÎ¿Ï… Î ÏÏ‰Ï„ÎµÏƒÎ¯Î»Î±Îµ\" four)"
</pre>
</div>

<p>
Do not forget that you will get an error if you are not quoting this
list at all, because the first element <code>one</code> will be treated as the
symbol of a defined function, which must then be called with all other
elements as its arguments. Chances are that <code>one</code> is not defined as a
function in your current Emacs session or those arguments are not
meaningful to it, anyway. Plus, <code>two</code> and <code>four</code> will then have to
exist as variables, since they are not quoted either. Else more errors
ensue. By quasi quoting the list, we get the best of both worlds:
evaluate what we need and skip the evaluation of all the rest.
</p>

<p>
Other than the comma operator, there is the <code>,@</code> (how is this even
pronounced? &ldquo;comma at&rdquo;, perhaps?), which is notation for &ldquo;splicing&rdquo;.
This is jargon in lieu of saying &ldquo;the return value is a list and I
want you to remove the outermost parentheses from it&rdquo;. In effect, the
code that would normally return <code>'(one two three)</code> now returns <code>one
two three</code>. This difference may not make much sense in a vacuum,
though it does once you consider those elements as expressions that
should work in their own right, rather than simply be elements of a
quoted list. I will not provide an example here, as I think this is
best covered in the context of defining macros (<a href="#h:evaluation-inside-of-a-macro-or-special-form">Evaluation inside of a macro or special form</a>).
</p>

<p>
Chances are you will not need to use the knowledge of selective
evaluation. It is more common in macros, though can be applied
anywhere. Be aware of it regardless, as there are scenaria where you
will, at the very least, want to understand what some code you depend
on is doing. Besides, when you write something like <code>`(one ,two
,three)</code> you can alternatively call the <code>list</code> function like <code>(list
'one two three)</code> to get the same results. Sometimes the backtick with
all its comma operators looks right while at others the <code>list</code> is more
appropriate. The way I approach this is in terms of style: what would
look more clean in the given context is the more appropriate option.
Mine is an aesthetic principle, if you will. In this example, I prefer
<code>(list 'one two three)</code> over <code>`(one ,two ,three)</code>: it has no special
syntax beside the basics of Lisp and is thus easier to reason about
(<a href="#h:basics-of-how-lisp-works">Basics of how Lisp works</a>).
</p>

<p>
Lastly, since I introduced you to some Greek words, I am now
considering you my potential friend. Here is a joke from when I was a
kid. I was trying to explain some event to my English instructor. As I
lacked the vocabulary to express myself, I resorted to the use of
Greek words. My instructor had a strict policy of only responding in
English, so she said &ldquo;It is all Greek to me&rdquo;. Not knowing that her
answer is an idiom for &ldquo;I do not understand you&rdquo;, I blithely replied,
&ldquo;Yes, madame, Greek; me no speak England very best.&rdquo; I was not
actually a beginner at the time. I simply could not pass on the
opportunity to make fun of the situation. This illustrates how you
should remember to enjoy the time spent tinkering with Emacs. But
enough of that! Back to the elements of Emacs Lisp.
</p>
</div>
</div>
<div id="outline-container-h:evaluation-inside-of-a-macro-or-special-form" class="outline-2">
<h2 id="h:evaluation-inside-of-a-macro-or-special-form"><span class="section-number-2">13.</span> Evaluation inside of a macro or special form</h2>
<div class="outline-text-2" id="text-h:evaluation-inside-of-a-macro-or-special-form">
<p>
In the most basic case of Emacs Lisp code, you have lists (<a href="#h:basics-of-how-lisp-works">Basics of how Lisp works</a>).
Those are either evaluated or not (<a href="#h:symbols-balanced-expressions-and-quoting">Symbols, balanced expressions, and quoting</a>).
If you get a little more fancy, you have lists whose elements are
selectively evaluated (<a href="#h:partial-evaluation-inside-of-a-list">Evaluate some elements inside of a list</a>).
Sometimes, though, you come across a piece of code and cannot
understand why the normal rules of quoting and evaluation do not
apply. Before you see this in action, inspect a typical function call
that also involves the evaluation of a variable:
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">(concat my-greeting-in-greek " " "Î ÏÏ‰Ï„ÎµÏƒÎ¯Î»Î±Îµ")
</pre>
</div>

<p>
You encountered this code in the section about the evaluation of some
elements inside of a list. What you have here is a call to the
function <code>concat</code>, followed by three arguments. One of these arguments
is a variable, the <code>my-greeting-in-greek</code>. When this list is
evaluated, what Emacs actually does is to first evaluate the
arguments, including <code>my-greeting-in-greek</code>, in order to resolve their
respective values. Once it collects those it calls <code>concat</code> with them.
You can think of the entire operation as follows:
</p>

<ul class="org-ul">
<li>Here is a list.</li>

<li>It is not quoted.</li>

<li>So you should evaluate it.</li>

<li>The first element is the name of the function.</li>

<li>The remaining elements are arguments passed to that function.</li>

<li>Check what the arguments are.</li>

<li>Evaluate each of the arguments to get its value.</li>

<li>Strings are self-evaluating (a string evaluates to itself), while
the <code>my-greeting-in-greek</code> is a variable that likely has some value
other than itself.</li>

<li>You now have the value of each of the arguments, including the value
of the symbol <code>my-greeting-in-greek</code>.</li>

<li>Call <code>concat</code> with all the values you got.</li>
</ul>

<p>
In other words, the following two yield the same results (assuming a
constant <code>my-greeting-in-greek</code>):
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">(concat my-greeting-in-greek " " "Î ÏÏ‰Ï„ÎµÏƒÎ¯Î»Î±Îµ")

(concat "Î“ÎµÎ¹Î± ÏƒÎ¿Ï…" " " "Î ÏÏ‰Ï„ÎµÏƒÎ¯Î»Î±Îµ")
</pre>
</div>

<p>
This is predictable. It follows the basic logic of the single quote:
if a symbol is quoted, do not evaluate it and return it as-is,
otherwise evaluate it and return its value. Here
<code>my-greeting-in-greek</code> is not quoted, so it is evaluated. All clear!
But you will find plenty of cases where this expected pattern is
seemingly not followed. Consider this common scenario of using <code>setq</code>
to bind a symbol to the given value (i.e. set the value of a
variable):
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">(setq my-test-symbol "Protesilaos of Cyprus")
</pre>
</div>

<p>
The above expression looks like a function call, meaning that (i) the
entire list is not quoted, (ii) the first element is the name of a
function, and (iii) the remaining elements are arguments passed to
that function. In a way, this is all true. Though you would then
expect the <code>my-test-symbol</code> to be treated as a variable, which would
be evaluated in place to return its result which would, in turn, be
the actual argument passed to the function. However, this is not how
<code>setq</code> works. The reason is that it is a so-called &ldquo;special form&rdquo; that
internally calls <code>set</code> while quoting the symbol it got, thus:
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">(set 'my-test-symbol "Protesilaos of Cyprus")
</pre>
</div>

<p>
With <code>set</code> things are as expected. There is no magic happening behind
the scenes. The <code>setq</code>, then, is a convenience for the user to not
quote the symbol each time. Yes, this makes it a bit more difficult to
reason about it, though you get used to it and eventually it all makes
sense. Hopefully, you will have a sense of when you are dealing with
special forms. Those include <code>setq</code>, <code>defun</code>, <code>let</code>, and a few others.
Here is a function you have already seen in this book:
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">(defun my-greet-person-from-country (name country)
  "Say hello to the person with NAME who lives in COUNTRY."
  (message "Hello %s of %s" name country))
</pre>
</div>

<p>
If the normal rules of evaluation applied, then the list of parameters
should be quoted. Otherwise, you would expect <code>(name country)</code> to be
interpreted as a function call with <code>name</code> as the symbol of the
function and <code>country</code> as its argument, which would also be a
variable. But this is not what is happening because <code>defun</code> will
internally treat that list of parameters as if it was quoted. Imagine
this:
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">;; PSEUDO-CODE to illustrate how the `defun' special form could be
;; expressed if normal quoting was applied.
(PSEUDO-defun 'my-greet-person-from-country '(name country)
  "Say hello to the person with NAME who lives in COUNTRY."
  (message "Hello %s of %s" name country))
</pre>
</div>

<p>
Now you can appreciate <code>defun</code> a little bit more.
</p>

<p>
Another common scenario is with <code>let</code> (<a href="#h:control-flow-with-if-let-and-friends">Control flow with <code>if-let*</code> and friends</a>).
Its general form is as follows:
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">;; This is pseudo-code
(let LIST-OF-LISTS-AS-VARIABLE-BINDINGS
  BODY-OF-THE-FUNCTION)
</pre>
</div>

<p>
The <code>LIST-OF-LISTS-AS-VARIABLE-BINDINGS</code> is a list in which each
element is a list of the form <code>(SYMBOL VALUE)</code>. Here is some actual
code:
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">(let ((name "Protesilaos")
      (country "Cyprus"))
  (message "Hello %s of %s" name country))
</pre>
</div>

<p>
Continuing with the theme of special forms, if <code>let</code> was a typical
function call, the <code>LIST-OF-LISTS-AS-VARIABLE-BINDINGS</code> would have to
be quoted. Otherwise, it would be evaluated, in which case the first
element would be the name of the function. But that would return an
error, as the name of the function would correspond to another list,
the <code>(name "Protesilaos")</code>, rather than a symbol. Things work fine
with <code>let</code> because it internally does the quoting of its
<code>LIST-OF-LISTS-AS-VARIABLE-BINDINGS</code>. Behold the pseudo-code that
illustrates my point:
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">(PSEUDO-let '((name "Protesilaos")
              (country "Cyprus"))
 (message "Hello %s of %s" name country))
</pre>
</div>

<p>
From a user perspective, there is no distinction between special forms
and Lisp macros. The former are implemented in C, as part of the
engine at the heart of Emacs. While Lisp macros are written in Emacs
Lisp, the same way as everything else you see in this book. In
practice, macros is how we define our own special form counterparts,
in the sense that macros can implement their own rules of evaluation.
A macro can even define its own mini-language, such that it looks
profoundly different from your average lisp expression. Depending on
how you feel about it, something like <code>cl-loop</code> will be a thing of
beauty or the bane of your existence (<a href="#h:pattern-match-with-pcase-and-related">Pattern match with <code>pcase</code> and related</a>).
</p>

<p>
Expect similar behaviour with many special forms as well as with
macros such as the popular <code>use-package</code>, which is used to configure
packages inside of your Emacs initialisation file. How each of those
macros works depends on the way it is designed. I will not delve into
the technicalities here, as I want the book to be useful long-term,
focusing on the principles rather than the implementation details that
might change over time. A cursory review of some <code>use-package</code>
declaration will suffice: you are not looking at your average Lisp
program and what you know may not even be valid syntax for this macro
(<a href="#h:basics-of-how-lisp-works">Basics of how Lisp works</a>).
</p>

<p>
To learn what a given macro expands to place the cursor at the end of
its closing parenthesis and call the command <code>pp-macroexpand-last-sexp</code>.
It will produce a new buffer showing the expanded Emacs Lisp code.
This is what is actually evaluated in the macro&rsquo;s stead.
</p>

<p>
With those granted, it is time to write a macro. This is like a
template, which empowers you to not repeat yourself. Syntactically, a
macro will most probably depend on the use of the backtick, the comma
operator, and the mechanics of splicing (<a href="#h:partial-evaluation-inside-of-a-list">Evaluate some elements inside of a list</a>).
Here is a simple scenario where we want to run some code in a
temporary buffer while setting the <code>default-directory</code> to the user&rsquo;s
home directory.
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">(defmacro my-work-in-temp-buffer-from-home (&amp;rest expressions)
  "Evaluate EXPRESSIONS in a temporary buffer with `default-directory' set to the user's home."
  `(let ((default-directory ,(expand-file-name "~/")))
     (with-temp-buffer
       (message "Running all expression from the `%s' directory" default-directory)
       ,@expressions)))
</pre>
</div>

<p>
In this definition, the <code>&amp;rest</code> keyword makes the following parameter
a list of arbitrary length. So you can pass any number of arguments to
it, all of which are collected into a single <code>expressions</code> variable.
The judicious use of selective evaluation inside of a quasi quoted
list ensures that the macro will not be evaluated right now but only
when its expansion is called. We thus appreciate how the quote is, in
effect a &ldquo;do not evaluate now but pass it over to the next evaluation&rdquo;
(<a href="#h:symbols-balanced-expressions-and-quoting">Symbols, balanced expressions, and quoting</a>). The arguments given to
it will be placed where you have specified. Here is a call that uses
this macro:
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">(progn
  (message "Now we are doing something unrelated to the macro")
  (my-work-in-temp-buffer-from-home
   (message "We do stuff inside the macro")
   (+ 1 1)
   (list "Protesilaos" "Cyprus")))
</pre>
</div>

<p>
If you place the cursor at the closing parenthesis of
<code>my-work-in-temp-buffer-from-home</code>, you will be able to confirm what
it expands to by typing M-x (<code>execute-extended-command</code>)
and then invoking the command <code>pp-macroexpand-last-sexp</code>. This is what
I get out of the macroexpansion:
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">(let ((default-directory "/home/prot/"))
  (with-temp-buffer
    (message "Running all expression from the `%s' directory" default-directory)
    (message "We do stuff inside the macro")
    (+ 1 1)
    (list "Protesilaos" "Cyprus")))
</pre>
</div>

<p>
Piecing it together with the rest of the code in its context, I arrive
at the following:
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">(progn
  (message "Now we are doing something unrelated to the macro")
  (let ((default-directory "/home/prot/"))
    (with-temp-buffer
      (message "Running all expression from the `%s' directory" default-directory)
      (message "We do stuff inside the macro")
      (+ 1 1)
      (list "Protesilaos" "Cyprus"))))
</pre>
</div>

<p>
With this example in mind, consider Elisp macros to be a way of saying
&ldquo;this little thing here helps me express this larger procedure more
succinctly, while the actual code that runs is still that of the
latter&rdquo;.
</p>

<p>
The above macro I wrote has its body start with a backtick, so you do
not get to appreciate the nuances of evaluation within it. Let me show
you this other approach, instead, where I write a macro that lets me
define several almost identical interactive functions (<a href="#h:make-your-interactive-function-also-work-from-lisp-calls">Make your interactive function also work from Lisp calls</a>).
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">(defmacro my-define-command (name &amp;rest expressions)
  "Define command with specifier NAME that evaluates EXPRESSIONS."
  (declare (indent 1))
  (unless (symbolp name)
    (error "I want NAME to be a symbol"))
  (let ((modifined-name (format "modified-version-of-%s" name)))
    `(defun ,(intern modifined-name) ()
       (interactive)
       ,(message "The difference between `%s' and `%s'" modifined-name name)
       ,@expressions)))
</pre>
</div>

<p>
The <code>my-define-command</code> can be broadly divided into two parts: (i)
what gets evaluated outright and (ii) what gets expanded for further
evaluation at a later point. The latter part starts with the backtick.
This distinction is important when we call the macro, because the
former part will be executed outright: if we hit the error, the macro
will never expand the rest of the form which includes the
<code>EXPRESSIONS</code>. Try <code>pp-macroexpand-last-sexp</code> with the following to
see what I mean. For your convenience, I include the macro expansions
right below each case.
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">(my-define-command first-demo
  (message "This is what my function does")
  (+ 1 10)
  (message "And this"))
;; =&gt;
;;
;; (defun modified-version-of-first-demo nil
;;   (interactive)
;;   "The difference between â€˜modified-version-of-first-demoâ€™ and â€˜first-demoâ€™"
;;   (message "This is what my function does")
;;   (+ 1 10)
;;   (message "And this"))


(my-define-command second-demo
  (list "Protesilaos" "Cyprus")
  (+ 1 1)
  (message "Arbitrary expressions here"))
;; =&gt;
;;
;; (defun modified-version-of-second-demo nil
;;   (interactive)
;;   "The difference between â€˜modified-version-of-second-demoâ€™ and â€˜second-demoâ€™"
;;   (list "Protesilaos" "Cyprus")
;;   (+ 1 1)
;;   (message "Arbitrary expressions here"))


(my-define-command "error scenario"
  (list "Will" "Not" "Reach" "This")
  (/ 2 0))
;; =&gt; ERROR...
</pre>
</div>

<p>
Do you need macros? Not always, though there will be cases where a
well-defined macro makes your code more elegant and, indeed, more
powerful. Experience will teach you when a macro is the right tool for
the job. What matters here is that you have a sense of how evaluation
works so that you do not get confused by all those parentheses,
quotes, quasi quoting, and splicing. Otherwise, you might expect
something different to happen than what you actually get. Use
<code>pp-macroexpand-last-sexp</code> meticulously and try to reason about what
it shows you.
</p>
</div>
</div>
<div id="outline-container-h:mapping-through-a-list-of-elements" class="outline-2">
<h2 id="h:mapping-through-a-list-of-elements"><span class="section-number-2">14.</span> Mapping over a list of elements</h2>
<div class="outline-text-2" id="text-h:mapping-through-a-list-of-elements">
<p>
A common routine in programming is to work through a list of items and
perform some computation on each of them. Emacs Lisp has the generic
<code>while</code> loop, as well as a whole range of more specialised functions
to map over a list of elements, such as <code>mapcar</code>, <code>mapc</code>, <code>dolist</code>,
<code>seq-filter</code>, <code>seq-remove</code>, and many more. Depending on what you are
doing, you map through elements with the intent to produce some side
effect and/or to test for a return value (<a href="#h:side-effect-and-return-value">Side effect and return value</a>).
I will show you some examples and let you decide which is the most
appropriate tool for the task at hand.
</p>

<p>
Starting with <code>mapcar</code>, it applies a function to each element of a
list. It then takes the return value at each iteration and collects it
into a new list. This is the return value of <code>mapcar</code> as a whole. In
the following code block, I use <code>mapcar</code> over a list of numbers to
increment them by <code>10</code> and return a new list of the incremented
numbers.
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">(mapcar
 (lambda (number)
   (+ 10 number))
 '(1 2 3 4 5))
;; =&gt; (11 12 13 14 15)
</pre>
</div>

<p>
In the code block above, I am using a <code>lambda</code>, else an anonymous
function (<a href="#h:when-to-use-a-named-function-or-a-lambda-function">When to use a named function or a lambda function</a>). Here is
the same code, but with an eponymous function, i.e. a named function:
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">(defun my-increment-by-ten (number)
  "Add 10 to NUMBER."
  (+ 10 number))

(mapcar #'my-increment-by-ten '(1 2 3 4 5))
;; =&gt; (11 12 13 14 15)
</pre>
</div>

<p>
Notice that here we quote the eponymous function (<a href="#h:symbols-balanced-expressions-and-quoting">Symbols, balanced expressions, and quoting</a>).
</p>

<p>
The <code>mapcar</code> collects the return values into a new list. Sometimes
this is useless. Suppose you want to evaluate a function that saves
all unsaved buffers which visit a file. In this scenario, you do not
care about accumulating the results: you just want the side effect of
saving the buffer outright. To this end, you may use <code>mapc</code>, which
always returns the list it operated on:
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">(mapc
 (lambda (buffer)
   (when (and (buffer-file-name buffer)
              (buffer-modified-p buffer))
     (save-buffer)))
 (buffer-list))
</pre>
</div>

<p>
An alternative to the above is <code>dolist</code>, which is used for side
effects but always returns <code>nil</code>:
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">(dolist (buffer (buffer-list))
  (when (and (buffer-file-name buffer)
             (buffer-modified-p buffer))
    (save-buffer)))
</pre>
</div>

<p>
You will notice that the <code>dolist</code> is a macro, so some parts of it seem
to behave differently than with basic lists and the evaluation rules
that apply to them (<a href="#h:basics-of-how-lisp-works">Basics of how Lisp works</a>). This is a matter of
getting used to how the code is expressed (<a href="#h:evaluation-inside-of-a-macro-or-special-form">Evaluation inside of a macro or special form</a>).
</p>

<p>
When to use a <code>dolist</code> as opposed to a <code>mapc</code> is largely a matter of
style. If you are using a named function, a <code>mapc</code> looks cleaner to my
eyes. Otherwise a <code>dolist</code> is easier to read. Here is my approach with
some pseudo-code:
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">;; I like this:
(mapc #'NAMED-FUNCTION LIST)

;; I also like a `dolist' instead of a `mapc' with a `lambda':
(dolist (element LIST)
  (OPERATE-ON element))

;; I do not like this:
(mapc
 (lambda (element)
   (OPERATE-ON element))
 LIST)
</pre>
</div>

<p>
While <code>dolist</code> and <code>mapc</code> are for side effects, you can still employ
them in the service of accumulating results, with the help of <code>let</code>
and related forms (<a href="#h:control-flow-with-if-let-and-friends">Control flow with <code>if-let*</code> and friends</a>). Depending
on the specifics, this approach may make more sense than relying on a
<code>mapcar</code>. Here is an annotated sketch:
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">;; Start with an empty list of `found-strings'.
(let ((found-strings nil))
  ;; Use `dolist' to test each element of the list '("Protesilaos" 1 2 3 "Cyprus").
  (dolist (element '("Protesilaos" 1 2 3 "Cyprus"))
    ;; If the element is a string, then `push' it to the `found-strings', else skip it.
    (when (stringp element)
      (push element found-strings)))
  ;; Now that we are done with the `dolist', return the new value of `found-strings'.
  found-strings)
;; =&gt; ("Cyprus" "Protesilaos")


;; As above but reverse the return value, which makes more sense:
(let ((found-strings nil))
  (dolist (element '("Protesilaos" 1 2 3 "Cyprus"))
    (when (stringp element)
      (push element found-strings)))
  (nreverse found-strings))
;; =&gt; ("Protesilaos" "Cyprus")
</pre>
</div>

<p>
For completeness, the previous example would have to be done as
follows with the use of <code>mapcar</code>:
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">(mapcar
 (lambda (element)
   (when (stringp element)
     element))
 '("Protesilaos" 1 2 3 "Cyprus"))
;; =&gt; ("Protesilaos" nil nil nil "Cyprus")


(delq nil
      (mapcar
       (lambda (element)
         (when (stringp element)
           element))
       '("Protesilaos" 1 2 3 "Cyprus")))
;; =&gt; ("Protesilaos" "Cyprus")
</pre>
</div>

<p>
Because <code>mapcar</code> happily accumulates all the return values, it returns
a list that includes <code>nil</code>. If you wanted that, you would probably not
even bother with the <code>when</code> clause there. The <code>delq</code> is thus applied
to the return value of the <code>mapcar</code> to delete all the instances of
<code>nil</code>. Now compare this busy work to <code>seq-filter</code>:
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">(seq-filter #'stringp '("Protesilaos" 1 2 3 "Cyprus"))
;; =&gt; ("Protesilaos" "Cyprus")
</pre>
</div>

<p>
The <code>seq-filter</code> is the best tool when all you need is to test if the
element satisfies a predicate function and then return that element.
But you cannot return something else. Whereas <code>mapcar</code> will take any
return value without complaints, such as the following:
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">(delq nil
      (mapcar
       (lambda (element)
         (when (stringp element)
           ;; `mapcar' accumulates any return value, so we can change
           ;; the element to generate the results we need.
           (upcase element)))
       '("Protesilaos" 1 2 3 "Cyprus")))
;; =&gt; ("PROTESILAOS" "CYPRUS")

(seq-filter
 (lambda (element)
   (when (stringp element)
     ;; `seq-filter' only returns elements that have a non-nil return
     ;; value here, but it returns the elements, not what we return
     ;; here.  In other words, this `lambda' does unnecessary work.
     (upcase element)))
 '("Protesilaos" 1 2 3 "Cyprus"))
;; =&gt; ("Protesilaos" "Cyprus")
</pre>
</div>

<p>
How you go about mapping over a list of elements will depend on what
you are trying to do. There is no one single function that does
everything for you. Understand the nuances and you are good to go. Oh,
and do look into the built-in <code>seq</code> library (use M-x
(<code>execute-extended-command</code>), invoke <code>find-library</code>, and then search
for <code>seq</code>). You are now looking at the source code of
seq.el: it defines plenty of functions like <code>seq-take</code>,
<code>seq-find</code>, <code>seq-union</code>. Another way is to invoke the command
<code>shortdoc</code> and read about the documentation groups <code>list</code> as well as
<code>sequence</code>.
</p>
</div>
</div>
<div id="outline-container-h:the-match-data-of-the-last-search" class="outline-2">
<h2 id="h:the-match-data-of-the-last-search"><span class="section-number-2">15.</span> The match data of the last search</h2>
<div class="outline-text-2" id="text-h:the-match-data-of-the-last-search">
<p>
As you work with Emacs Lisp, you will encounter the concept of &ldquo;match
data&rdquo; and the concomitant functions <code>match-data</code>, <code>match-beginning</code>,
<code>match-string</code>, and so on. These refer to the results of the last
search, which is typically performed by the functions <code>looking-at</code>,
<code>re-search-forward</code>, <code>string-match</code>, and related. Each time you
perform a search, the match data gets updated. Be mindful of this
common side effect (<a href="#h:side-effect-and-return-value">Side effect and return value</a>). If you forget about
it, chances are your code will not do the right thing.
</p>

<p>
In the following code block, I define a function that performs a
search in the current buffer and returns a list of match data without
text properties, where relevant (<a href="#h:text-has-its-own-properties">Text has its own properties</a>).
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">(defun my-get-match-data (regexp)
  "Search forward for REGEXP and return its match data, else nil."
  (when (re-search-forward regexp nil t)
    (list
     :beginning (match-beginning 0)
     :end (match-end 0)
     :string (match-string-no-properties 0))))
</pre>
</div>

<p>
You may then call it with a string argument, representing an Emacs
Lisp regular expression:
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">(my-get-match-data "Protesilaos.*Cyprus")
</pre>
</div>

<p>
If the regular expression matches, then you get the match data. Here
is some sample text:
</p>

<div class="org-src-container">
<pre class="src src-fundamental">Protesilaos lives in the mountains of Cyprus.
</pre>
</div>

<p>
Place the cursor before that text and use M-:
(<code>eval-expression</code>) to evaluate <code>my-get-match-data</code> with the regexp I
show above. You will get a return value, as intended.
</p>

<p>
The way <code>my-get-match-data</code> is written, it does two things: (i) it has
the side effect of moving the cursor to the end of the text it found
and (ii) it returns a list with the match data I specified. There are
many scenaria where you do not want the aforementioned side effect:
the cursor should stay where it is. As such, you can wrap your code in
a <code>save-excursion</code> (<a href="#h:switching-to-another-buffer-window-or-narrowed-state">Switching to another buffer, window, or narrowed state</a>):
it will do what it must and finally restore the <code>point</code> (<a href="#h:run-some-code-or-fall-back-to-some-other-code">Run some code or fall back to some other code</a>):
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">(defun my-get-match-data (regexp)
  "Search forward for REGEXP and return its match data, else nil."
  (save-excursion ; we wrap our code in a `save-excursion' to inhibit the side effect
    (when (re-search-forward regexp nil t)
      (list
       :beginning (match-beginning 0)
       :end (match-end 0)
       :string (match-string-no-properties 0)))))
</pre>
</div>

<p>
If you evaluate this version of <code>my-get-match-data</code> and then retry the
function call I had above, you will notice how you get the expected
return value without the side effect of the cursor moving to the end
of the matching text. In practice, this is a useful tool that may be
combined with <code>save-match-data</code>. Imagine you want to do a search
forward inside of another search you are performing, such as to merely
test if there is a match for a regular expression in the context, but
need to inhibit the modification of the match data you planned to
operate on. As such:
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">(defun my-get-match-data-with-extra-check (regexp)
  "Search forward for REGEXP followed by no spaces and return its match data, else nil."
  (save-excursion
    (when (and (re-search-forward regexp nil t)
               (save-match-data (not (looking-at "[\s\t]+"))))
      ;; Return the match data of the first search.  The second one
      ;; which tests for spaces or tabs is just an extra check, but we
      ;; do not want to use its match data, hence the `save-match-data'
      ;; around it.
      (list
       :beginning (match-beginning 0)
       :end (match-end 0)
       :string (match-string-no-properties 0)))))
</pre>
</div>

<p>
Evaluate the function <code>my-get-match-data-with-extra-check</code> and then
call it with M-: (<code>eval-expression</code>) to test that it
returns a non-<code>nil</code> value with the second example below, but not the
first one. This is the expected outcome.
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">(my-get-match-data-with-extra-check "Protesilaos.*Cyprus")
;; =&gt; nil


;; Protesilaos, also known as "Prot", lives in the mountains of Cyprus   .

(my-get-match-data-with-extra-check "Protesilaos.*Cyprus")
;; =&gt; (:beginning 41988 :end 42032 :string "Protesilaos lives in the mountains of Cyprus")


;; Protesilaos lives in the mountains of Cyprus.
</pre>
</div>

<p>
If all you want is to check for a regular expression in the buffer,
you wrap your search in a <code>save-match-data</code>. If, on the other hand,
you need to test for the presence of a regular expression in a string,
without modifying&#x2014;or indeed wanting to access&#x2014;the match, then you
can rely on the function <code>string-match-p</code>. Given that much of the work
you do in Emacs involves buffers, it is important to familiarise
yourself with how match data work (<a href="#h:buffers-as-data-structures">Buffers as data</a>). Another use-case
for match data is if you are writing fontification rules for a major
or minor mode, though that is probably more of a niche requirement
(<a href="#h:what-are-major-and-minor-modes">What are major and minor modes</a>).
</p>
</div>
</div>
<div id="outline-container-h:switching-to-another-buffer-window-or-narrowed-state" class="outline-2">
<h2 id="h:switching-to-another-buffer-window-or-narrowed-state"><span class="section-number-2">16.</span> Switching to another buffer, window, or narrowed state</h2>
<div class="outline-text-2" id="text-h:switching-to-another-buffer-window-or-narrowed-state">
<p>
As you use Emacs Lisp to do things programmatically, you encounter
cases where you need to move away from where you are in order to
perform some computation. You may have to switch to another buffer,
change to the window of a given buffer, or even modify what is visible
in the buffer you are editing. At all times, this involves one or more
side effects which, most probably, should be undone once your program
concludes its operations (<a href="#h:side-effect-and-return-value">Side effect and return value</a>).
</p>

<p>
Perhaps the most common case is to restore the <code>point</code>. You have some
code that moves back or forth in the buffer to perform a match for a
given piece of text. But then, you need to leave the cursor where it
originally was, otherwise the user will lose their orientation. Wrap
your code in a <code>save-excursion</code> and you are good to go, as I show
elsewhere (<a href="#h:the-match-data-of-the-last-search">The match data of the last search</a>):
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">(save-excursion ; restore the `point' after you are done
  MOVE-AROUND-IN-THIS-BUFFER)
</pre>
</div>

<p>
Same principle for <code>save-window-excursion</code>, which allows you to select
another window, such as with <code>select-window</code>, move around in its
buffer, and then restore the windows as they were:
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">(save-window-excursion
  (select-window SOME-WINDOW)
  MOVE-AROUND-IN-THIS-BUFFER)
</pre>
</div>

<p>
The <code>save-restriction</code> allows you to restore the current visibility
state of the buffer. This pertains to whether the buffer is narrowed
or not and the exact boundaries of the narrowed view. When the buffer
is narrowed, the functions <code>point-min</code> and <code>point-max</code> return the
minimum and maximum positions within those boundaries. Your program
may need to work outside of them, yet still respect them after it is
done. Same principle if you need to narrow to some portion of the
buffer to better target a piece of text. You may then choose to either
<code>widen</code> or <code>narrow-to-region</code> (and related commands like
<code>org-narrow-to-subtree</code>), do what you must, and then restore the
buffer to its original state.
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">;; Here we assume that we start in a widened state.  Then we narrow to
;; the current Org heading to get all of its contents as one massive
;; string.  Then we widen again, courtesy of `save-restriction'.
(save-restriction
  (org-narrow-to-subtree)
  (buffer-string))
</pre>
</div>

<p>
Depending on the specifics, you will want to combine the
aforementioned. Beware that the documentation of <code>save-restriction</code>
tells you to use <code>save-excursion</code> as the outermost call. Other than
that, you will also find cases that require a different approach to
perform some conditional behaviour (<a href="#h:run-some-code-or-fall-back-to-some-other-code">Run some code or fall back to some other code</a>).
</p>

<p>
At any rate, because Emacs effectively is one large mutable state, you
have to write code that does not produce permanent side effects that
are not necessary. Guard against them and you should be good to go
(<a href="#h:emacs-as-a-computing-environment">Emacs as a computing environment</a>).
</p>
</div>
</div>
<div id="outline-container-h:basic-control-flow-with-if-cond-and-others" class="outline-2">
<h2 id="h:basic-control-flow-with-if-cond-and-others"><span class="section-number-2">17.</span> Basic control flow with <code>if</code>, <code>cond</code>, and others</h2>
<div class="outline-text-2" id="text-h:basic-control-flow-with-if-cond-and-others">
<p>
You do not need any conditional logic to perform basic operations. For
example, if you write a command that moves 15 lines down, it will
naturally stop at the end of the buffer when it cannot move past the
number you specified. Using <code>defun</code>, you write an interactive function
(i.e. a &ldquo;command&rdquo;) to unconditionally move down 15 lines using
<code>forward-line</code> internally (<a href="#h:basics-of-how-lisp-works">Basics of how Lisp works</a>).
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">(defun my-15-lines-down ()
  "Move at most 15 lines down."
  (interactive)
  ;; Give it a negative number to move in the opposite direction.
  (forward-line 15))
</pre>
</div>

<p>
The <code>my-15-lines-down</code> is about as simple as it gets: it wraps around
a basic function and passes to it a fixed argument, in this case the
number <code>15</code>. Use M-x (<code>execute-extended-command</code>) and then
call this command by its name. It works! Things get more involved as
soon as you decide to perform certain actions only once a given
condition is met. This &ldquo;control flow&rdquo; between different branches of a
logical sequence is expressed with <code>if</code>, <code>when</code>, <code>unless</code>, and <code>cond</code>,
among others. Depending on the specifics of the case, <code>and</code> as well as
<code>or</code> may suffice.
</p>

<p>
How about you make your <code>my-15-lines-down</code> a bit smarter? When it is
at the absolute end of the buffer, have it move 15 lines up. Why?
Because this is a demonstration, so why not? The predicate function
that tests if the point is at the end of the buffer is <code>eobp</code>. A
&ldquo;predicate&rdquo; is a function that returns true, technically non-<code>nil</code>,
when its condition is met, else it returns <code>nil</code> (<a href="#h:side-effect-and-return-value">Side effect and return value</a>).
As for the weird name, the convention in Emacs Lisp is to end
predicate functions with the <code>p</code> suffix: if the name of the function
consists of multiple words, typically separated by dashes, then the
predicate function is named <code>NAME-p</code>, like <code>string-match-p</code>, otherwise
it is <code>NAMEp</code>, like <code>stringp</code>.
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">(defun my-15-lines-down-or-up ()
  "Move at most 15 lines down or go back if `eobp' is non-nil."
  (interactive)
  (if (eobp)
      (forward-line -15)
    (forward-line 15)))
</pre>
</div>

<p>
Evaluate this function, then type M-x (<code>execute-extended-command</code>)
and invoke <code>my-15-lines-down-or-up</code> to get a feel for it. Below is a
similar idea, which throws and error and exits what it was doing if
<code>eobp</code> returns non-<code>nil</code>:
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">(defun my-15-lines-down-or-error ()
  "Throw an error if `eobp' returns non-nil, else move 15 lines down."
  (interactive)
  (if (eobp)
      (error "Already at the end; will not move further")
    (forward-line 15)))
</pre>
</div>

<p>
A peculiarity of Emacs Lisp is how indentation is done. Just mark the
code you have written and type TAB: Emacs will take care to
indent it the way it should be done. Do not leave parentheses on their
own line. Put them at the end of a balanced expression, as you have
seen in all my examples. In the case of the <code>if</code> statement, the &ldquo;then&rdquo;
part is further in than the &ldquo;else&rdquo; part of the logic. There is no
special meaning to this indentation: you could write everything on a
single line like <code>(if COND THIS ELSE)</code>, which looks like your typical
list, by the way (<a href="#h:symbols-balanced-expressions-and-quoting">Symbols, balanced expressions, and quoting</a>).
</p>

<p>
What the indentation does is help you identify imbalances in your
parentheses. If the different expressions all line up in a way that
looks odd, then you are most probably missing a parenthesis or have
too many of them. Generally, expressions at the same level of depth
will all line up the same way. Those deeper in will have more
indentation, and so on. Experience will allow you to spot mistakes
with mismatching parentheses. But even if you do not identify them,
you will get an error eventually. Rest assured!
</p>

<p>
The way <code>if</code> is written is like a function that takes two or more
arguments (<a href="#h:evaluation-inside-of-a-macro-or-special-form">Evaluation inside of a macro or special form</a>). The &ldquo;or
more&rdquo; counts as part of the &ldquo;else&rdquo; logic. As such, <code>(if COND THIS)</code>
has no &ldquo;else&rdquo; consequence, while <code>(if COND THIS ELSE1 ELSE2 ELSE3)</code>
will run <code>ELSE1</code>, <code>ELSE2</code>, and <code>ELSE3</code> in order as part of the &ldquo;else&rdquo;
branch. Here is how this looks once you factor in proper indentation:
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">(if COND
    THIS
  ELSE1
  ELSE2
  ELSE3)
</pre>
</div>

<p>
Now what if the <code>THIS</code> part needs to be more than one function call?
Elisp has the <code>progn</code> form, which you can use to wrap function calls
and pass them as a single argument. Putting it all together, your code
will now look like this:
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">(if COND
    (progn
      THIS1
      THIS2
      THIS3)
  ELSE1
  ELSE2
  ELSE3)
</pre>
</div>

<p>
The else clause does not need a <code>progn</code> because internally it is
functionally equivalent to using a <code>&amp;rest</code> for the arguments. You may
remember some of the examples I demonstrated with <code>&amp;rest</code> (<a href="#h:hooks-and-the-advice-mechanism">Hooks and the advice mechanism</a>).
In short, it will automatically collect all arguments into a single
list. But because we can only have one <code>&amp;rest</code> parameter, the first
argument passed to <code>if</code> cannot be done the same as the else. Besides,
how would we be able to disambiguate something like this?
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">(if COND ONE TWO THREE)
</pre>
</div>

<p>
Without knowing where the arguments start and end, we cannot have a
reliable way forward. Whereas this is clear:
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">(if COND (progn ONE TWO) THREE)
</pre>
</div>

<p>
This would also work, but is unnecessary:
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">(if COND (progn ONE TWO) (progn THREE FOUR))

;; Same as above because of how there is a de facto &amp;rest for the "else" consequences
(if COND (progn ONE TWO) THREE FOUR)
</pre>
</div>

<p>
If you do not need the &ldquo;else&rdquo; part, use <code>when</code> to express your
intention. Internally, this is a macro which actually stands for <code>(if
COND (progn EXPRESSIONS))</code>, where <code>EXPRESSIONS</code> is one or more
expressions. A <code>when</code> looks like this:
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">(when COND
  THIS1
  THIS2
  THIS3)
</pre>
</div>

<p>
I personally always opt for <code>when</code> instead of writing an <code>if</code> without
an &ldquo;else&rdquo; branch, because this way I am clear about what I am trying
to do. When I look back at my code, I do not need to double-check
every <code>if</code> statement to confirm that it is balanced the way it ought
to.
</p>

<p>
Similarly, the <code>unless</code> has the meaning of <code>(when (not COND)
EXPRESSIONS)</code>. It, too, is a macro that expands to an <code>if</code> statement
and, again, I rely on it whenever I want to make my intent clear:
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">(unless COND
  THIS1
  THIS2
  THIS3)
</pre>
</div>

<p>
When the condition you are testing for has multiple parts, you can
rely on <code>and</code> as well as <code>or</code>:
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">(when (or THIS THAT)
  EXPRESSIONS)

(when (and THIS THAT)
  EXPRESSIONS)

(when (or (and THIS THAT) OTHER)
  EXPRESSIONS)
</pre>
</div>

<p>
Depending on what you are doing, you may not even need to write
anything more than <code>and</code> or <code>or</code>. For example, suppose you want to
express the logic of "if something is true, return it, else return an
error. You may write this using an <code>if</code>, but an <code>or</code> will also do,
thus:
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">;; Return THIS if it is non-nil
(if THIS
    THIS
  (error "The THIS cannot be nil"))

;; This is practically the same as above
(or THIS
    (error "The THIS cannot be nil"))
</pre>
</div>

<p>
Like <code>or</code>, <code>and</code> can also do the same trick:
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">;; These are practically the same.
(when (and THIS THAT)
  (FUNCTION-FOR THIS THAT))

(and THIS
     THAT
     (FUNCTION-FOR THIS THAT))
</pre>
</div>

<p>
Depending on the specifics of the case, the combination of multiple
<code>if</code>, <code>when</code>, <code>or</code>, <code>and</code> will look awkward. You can break down the
logic into distinct conditions, which are tested in order from top to
bottom, using <code>cond</code>. The way <code>cond</code> is written is as a list of lists,
which do not need quoting because <code>cond</code> is a special form (<a href="#h:evaluation-inside-of-a-macro-or-special-form">Evaluation inside of a macro or special form</a>).
In abstract, it looks like this:
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">(cond
 (CONDITION1
  CONSEQUENCES1)
 (CONDITION2
  CONSEQUENCES2)
 (CONDITION3
  CONSEQUENCES3)
 (t
  CONSEQUENCES-FALLBACK))
</pre>
</div>

<p>
Each of the consequences can be any number of expressions, like you
saw above with <code>when</code> or the <code>if COND (progn EXPRESSIONS)</code> pattern.
This is a toy function to show how <code>cond</code> behaves:
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">(defun my-toy-cond (argument)
  "Return a response depending on the type of ARGUMENT."
  (cond
   ((and (stringp argument)
         (string-blank-p argument))
    (message "You just gave me a blank string; try harder!"))
   ((stringp argument)
    (message "I see you can do non-blanks string; I call that progress."))
   ((null argument)
    (message "Yes, the nil is an empty list like (), but do not worry about it"))
   ((listp argument)
    (message "Oh, I see you are in the flow of using lists!"))
   ((symbolp argument)
    (message "What's up with the symbols, mate?"))
   ((natnump argument)
    (message "I fancy those natural numbers!"))
   ((numberp argument)
    (message "You might as well be a math prodigy!"))
   (t
    (message "I have no idea what type of thing your argument `%s' is" argument))))
</pre>
</div>

<p>
I want you to evaluate it and pass it different arguments to test what
it does (<a href="#h:evaluate-emacs-lisp">Evaluate Emacs Lisp</a>). Here are two examples:
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">(my-toy-cond "")
;; =&gt; "You just gave me a blank string; try harder!"

(my-toy-cond '(1 2 3))
;; =&gt; "Oh, I see you are in the flow of using lists!"
</pre>
</div>

<p>
All of the above are common in Emacs Lisp. Another powerful macro is
<code>pcase</code>, which we will consider separately due to its particularities
(<a href="#h:pattern-match-with-pcase-and-related">Pattern match with <code>pcase</code> and related</a>).
</p>
</div>
</div>
<div id="outline-container-h:control-flow-with-if-let-and-friends" class="outline-2">
<h2 id="h:control-flow-with-if-let-and-friends"><span class="section-number-2">18.</span> Control flow with <code>if-let*</code> and friends</h2>
<div class="outline-text-2" id="text-h:control-flow-with-if-let-and-friends">
<p>
A function may need to process data that is local to it; data that is
located in the context of the function, else in its &ldquo;lexical scope&rdquo;.
This means that whatever variables or values thereof should not be
exposed to the environment outside of the function. The <code>let</code> and
<code>let*</code> create and bind variables that are available only within their
scope, else the <code>BODY</code> of the <code>let</code>. These bindings can be arbitrary
new symbols or existing symbols with a new value. Those will take
precedence over the value of the same variable outside of this lexical
scope. Again, you will notice how Emacs is consistent about the
principle of the specific/local taking precedence over the
generic/global (<a href="#h:what-are-major-and-minor-modes">What are major and minor modes</a>). As such:
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">(let BINDINGS
  BODY)

(let ((variable1 value1)
      (variable2 value2))
  BODY)
</pre>
</div>

<p>
The <code>BINDINGS</code> is a list of lists, which does not need to be quoted
(<a href="#h:evaluation-inside-of-a-macro-or-special-form">Evaluation inside of a macro or special form</a>). While <code>BODY</code> consists
of one or more expressions, which I have also named <code>EXPRESSIONS</code>
elsewhere in this book. The difference between <code>let</code> and <code>let*</code>
(pronounced &ldquo;let star&rdquo;) is that the latter makes earlier bindings
available to later bindings. Like this:
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">;; This works because `greeting' can access `name' and `country',
;; courtesy of `let*':
(let* ((name "Protesilaos")
       (country "Cyprus")
       (greeting (format "Hello %s of %s" name country)))
  (DO-STUFF-WITH greeting))

;; But this fails...
(let ((name "Protesilaos")
      (country "Cyprus")
      (greeting (format "Hello %s of %s" name country)))
  (DO-STUFF-WITH greeting))
</pre>
</div>

<p>
Sometimes what you want to do is create those bindings if&#x2014;and only
if&#x2014;they are all non-<code>nil</code>. If their value is <code>nil</code>, then they are
useless to you, in which case you do something else (<a href="#h:basic-control-flow-with-if-cond-and-others">Basic control flow with <code>if</code>, <code>cond</code>, and others</a>).
Values may or may not be <code>nil</code> when you are creating a binding with
the return value of a function call or some other variable. You could
always write code like this:
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">(let ((variable1 (SOME-FUNCTION SOME-ARGUMENT))
      (variable2 (OTHER-FUNCTION OTHER-ARGUMENT)))
  (if (and variable1 variable2) ; simply test both for non-nil
      THIS
    ELSE))
</pre>
</div>

<p>
But you can do the same with <code>if-let*</code>, where the <code>THIS</code> part runs
only if all the bindings are non-<code>nil</code>:
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">(if-let* ((variable1 (SOME-FUNCTION SOME-ARGUMENT))
          (variable2 (OTHER-FUNCTION OTHER-ARGUMENT)))
    THIS
  ELSE)
</pre>
</div>

<p>
The <code>when-let*</code> is the same as <code>when</code>, meaning that it has no &ldquo;else&rdquo;
logic. If one of its bindings is <code>nil</code>, then the whole <code>when-let*</code>
returns <code>nil</code> immeditately.
</p>

<p>
The bindings of both <code>if-let*</code> and <code>when-let*</code> have an implied <code>and</code>
logic to them, exactly because they do not accept a <code>nil</code> value: as
soon as a <code>nil</code> is returned, they are aborted. Concretely, if a
<code>if-let*</code> has three bindings and the second is <code>nil</code>, the third will
never be bound to what you expect. In the following example, the
<code>if-let*</code> has the third binding of <code>(country "Cyprus")</code> which,
however, is not established because <code>if-let*</code> exits at the <code>nil</code> it
gets from its second binding. Consequently, the <code>ELSE</code> branch of the
logic get <code>nil</code> out of <code>country</code> instead of what we thought we were
assigning it to.
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">(if-let* ((name "Protesilaos")
          (nickname nil)
          (country "Cyprus"))
    (message "This does not run")
  (message "The country is %s" country))
;; =&gt; "The country is nil"
</pre>
</div>

<p>
Remember this implicit <code>and</code> for the <code>if-let*</code> and <code>when-let*</code>
bindings, otherwise your code will not do what you expect.
</p>

<p>
As you dig deeper into the Emacs Lisp ecosystem, you will come across
uses of <code>if-let*</code> or <code>when-let*</code> that (i) create multiple bindings
like <code>let</code> or <code>let*</code> but (ii) also call a predicate function to test
if they should continue with the <code>THIS</code> part of their logic. Remember
that <code>if-let*</code> goes straight to <code>ELSE</code> if one of its bindings returns
<code>nil</code>, while <code>when-let*</code> exits immediately and returns <code>nil</code>. Consider
this example:
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">(if-let* ((variable1 (SOME-FUNCTION SOME-ARGUMENT))
          ;; The _ signifies intent: "do not bind this; I only care
          ;; about the return value being non-nil".  What we are doing
          ;; here is test if `variable1' is a string: if it is, we
          ;; continue with the bindings, otherwise we move to the ELSE
          ;; part of the code and thus refrain from binding
          ;; `variable2' to whatever it would otherwise be bound to.
          (_ (stringp variable1))
          (variable2 (OTHER-FUNCTION OTHER-ARGUMENT)))
    THIS
  ELSE)
</pre>
</div>

<p>
I personally enjoy using <code>if-let*</code> and <code>when-let*</code>. They have the same
syntax as <code>let*</code>, which means that I do not need to remember the
intricacies of yet another Lisp macro. Plus, they express intent which
is always good as a way of making the code self-evident or, at least,
easier to reason about. Ultimately though, there is no inherently
superior way of doing things. It is a matter of using the right tool
for the task at hand. Sometimes you want the bindings to be created,
even if their value is <code>nil</code>, in which case <code>let</code> or <code>let*</code> are what
you need. Choose what makes sense.
</p>
</div>
</div>
<div id="outline-container-h:autoloading-symbols" class="outline-2">
<h2 id="h:autoloading-symbols"><span class="section-number-2">19.</span> Autoloading symbols</h2>
<div class="outline-text-2" id="text-h:autoloading-symbols">
<p>
In general, Emacs knows the functions and variables it has evaluated
(<a href="#h:evaluate-emacs-lisp">Evaluate Emacs Lisp</a>). Though it is also aware of all symbols that are
marked for automatic loading inside the <code>load-path</code>. The <code>load-path</code>
is a variable whose value is a list of directories. Each of those
directories is expected to contain Elisp files. Those are specified
with the <code>.el</code> file type extension, such as my-hello.el.
An Elisp file may choose to mark for automatic loading some of the
symbols it defines. When a symbol is marked for automatic loading,
also known as an &ldquo;autoload&rdquo;, Emacs will do the following when that
symbol is called for evaluation:
</p>

<ul class="org-ul">
<li>If the file with the given symbol&rsquo;s source code have been loaded,
then Emacs will simply evaluate the symbol in the given call.</li>

<li>If the file has not been loaded, Emacs will first load and evaluate
the file, and then evaluate the symbol from where it is being
called.</li>
</ul>

<p>
In this way, we can write programs that load their source code only
when needed. Such is a concept that is also described by members of
the community as &ldquo;lazy loading&rdquo;. The idea is that we do not need to
evaluate potentially millions of lines of code as soon as we launch
Emacs. Most of that is likely not needed each time. But when we wish
to do something with one of those definitions, its code base becomes
available to us.
</p>

<p>
Programs will typically have functions that are there purely for
internal purposes and others which are meant to be called by the user.
The latter may be autoloaded. Though the programmer must take care to
autoload only what is the actual point of entry to the package and not
every single symbol that may be called at some point. For example, the
built-in ielm.el autoloads the command <code>ielm</code>. This makes
perfect sense. If we want to start using that package, we will do it
by invoking <code>ielm</code>. The program defines many other commands, such as
<code>ielm-return</code>. Those only work inside of the IELM shell. It thus is
wrong to autoload them and, indeed, <code>ielm-return</code> and all related
commands are not autoloaded. Compare the differences by doing the
following:
</p>

<ul class="org-ul">
<li>Call the command <code>find-library</code>. It will ask you for a file anywhere
in the <code>load-path</code> that contains Emacs Lisp.</li>

<li>Select <code>ielm</code> to visit the file ielm.el.</li>

<li>Search for <code>(defun ielm (</code>. Notice that the definition of this
command has a special <code>;;;###autoload</code> directive right above it.</li>

<li>Now search for <code>(defun ielm-return</code>. This one does not have the
<code>;;;###autoload</code>.</li>
</ul>

<p>
In this case, <code>ielm</code> is declared for autoloading, while <code>ielm-return</code>
is not. The latter will be available only after the former has been
called (or the entire feature is explicitly loaded by the user or
another program). This is the correct way to organise the code of a
program.
</p>

<p>
From the programmer&rsquo;s persective, autoloading is set up with those
special <code>;;;###autoload</code> comments. For example:
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">;;;###autoload
(defun my-hello-world ()
  "Greet the world."
  (message "Hello everyone! My name is Protesilaos, also known as Prot!"))
</pre>
</div>

<p>
If the code you want to autoload is the result of a macro, then you
would need to write it this way (<a href="#h:evaluation-inside-of-a-macro-or-special-form">Evaluation inside of a macro or special form</a>):
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">;;;###autoload (autoload 'my-hello-world "my-hello")
(my-hello-define-command ARGUMENTS-FOR-THE-MACRO-THAT-WILL-YIELD-my-hello-world-DEFINITION)
</pre>
</div>

<p>
Here the <code>my-hello-define-command</code> is an Elisp macro that expands into
a function like the <code>my-hello-world</code> shown further above. Because it
is the latter you wish to autoload, you need to write the exact
function call that would yield the desired result, hence
<code>;;;###autoload (autoload 'my-hello-world "my-hello")</code>.
</p>

<p>
When you write <code>;;;###autoload</code> on its own, you are effectively asking
to operate on the following symbol being defined. Whereas an
<code>;;;###autoload WHAT-TO-LOAD-EXACTLY</code> gives you complete control over
what to autoload by specifying the exact function call to be used.
Generally, <code>;;;###autoload</code> is enough.
</p>

<p>
The <code>;;;###autoload</code> statements are read by the function
<code>loaddefs-generate</code>. This is typically done when you install a
package, such as with the <code>package-install</code> command. The relevant
machinery will read every <code>.el</code> file to generate a new file that
contains all the autoloads. This file will be in the same directory as
the rest of the source code, all of which is part of a directory among
those specified in the <code>load-path</code>.
</p>

<p>
If you have ever installed a package, then you most likely have files
with autoloads. You can check what they look like by doing the
following:
</p>

<ul class="org-ul">
<li>Visit the directory where you Emacs configuration file is. It might
be at <code>~/.emacs.d/</code> or <code>~/.config/emacs/</code>. When in doubt, inspect
the value of the variable <code>user-emacs-directory</code>.</li>

<li>If you have installed packages, you will find an <code>elpa</code> directory.</li>

<li>Therein are all the directories of the packages you have.</li>

<li>Inside those you will find <code>.el</code> files, which are the original
source code, <code>.elc</code> files which are the byte compiled counterparts
of the former, and a <code>PACKAGE-autoloads.el</code> file with all the
collected autoloads. Here <code>PACKAGE</code> is a placeholder for the name of
the package.</li>
</ul>

<p>
When you start up Emacs, it reads all those PACKAGE-autoloads.el
and thus knows where the specified symbols are defined. From then on,
the lazy loading happens when it must, i.e. when the affected symbol
is called for evaluation.
</p>

<p>
Every Emacs user experiences the benefits of autoloading from day one.
Consider the scenario where you launch a pristine Emacs for the first
time. This can be done at any moment by running the following on the
command-line or terminal emulator:
</p>

<div class="org-src-container">
<pre class="src src-sh">emacs -q
</pre>
</div>

<p>
This generic Emacs session should take a short amount of time to
initialise its state and be ready for further input. Do M-x
(<code>execute-extended-command</code>) and press the TAB key. A
<code>*Completions*</code> buffer will pop up, informing you about the thousands
of commands that are available as candidates of the present
interactive function call. Emacs has not actually evaluated the code
of all those: that would take a lot more time than what you
experienced at startup. Instead, Emacs is aware of all of them via the
autoload mechanism. Once you invoke a command, Emacs will take care to
load the corresponding file and then perform whatever computation the
command specifies (<a href="#h:make-your-interactive-function-also-work-from-lisp-calls">Make your interactive function also work from Lisp calls</a>).
</p>

<p>
What is usually autoloaded are commnads, though variables can be
handled the same way. It all depends on what the program is doing.
</p>
</div>
</div>
<div id="outline-container-h:pattern-match-with-pcase-and-related" class="outline-2">
<h2 id="h:pattern-match-with-pcase-and-related"><span class="section-number-2">20.</span> Pattern match with <code>pcase</code> and related</h2>
<div class="outline-text-2" id="text-h:pattern-match-with-pcase-and-related">
<p>
Once you get in the flow of expressing your thoughts with Emacs Lisp,
you will be fluent in the use of <code>if</code>, <code>cond</code>, and the like (<a href="#h:basic-control-flow-with-if-cond-and-others">Basic control flow with <code>if</code>, <code>cond</code>, and others</a>).
You might even get more fancy with <code>if-let*</code> and <code>when-let*</code> (<a href="#h:control-flow-with-if-let-and-friends">Control flow with <code>if-let*</code> and friends</a>).
However you go about it, there are some cases that arguably benefit
from more succinct expressions. &ldquo;Arguably&rdquo; is the operative term,
because code that is more terse can also be harder to reason about. At
any rate, this is where the <code>pcase</code> Lisp macro comes in (<a href="#h:evaluation-inside-of-a-macro-or-special-form">Evaluation inside of a macro or special form</a>).
At its more basic formulation, it is like <code>cond</code>, in that it tests the
return value of a given expression against a list of conditions.
Unlike <code>cond</code>, it can perform pattern matching and create binding that
it can then use based on the pattern it is matching.
</p>

<p>
Let me show you how <code>pcase</code> works like a <code>cond</code>. Here is an example
that compares the buffer-local value of the variable <code>major-mode</code> for
equality against a couple of known major mode symbols, namely
<code>org-mode</code> and <code>emacs-lisp-mode</code> (<a href="#h:what-are-major-and-minor-modes">What are major and minor modes</a>):
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">(pcase major-mode
  ('org-mode (message "You are in Org"))
  ('emacs-lisp-mode (message "You are in Emacs Lisp"))
  (_ (message "You are somewhere else")))
</pre>
</div>

<p>
The above is the same idea as the following <code>cond</code>:
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">(cond
 ((eq major-mode 'org-mode)
  (message "You are in Org"))
 ((eq major-mode 'emacs-lisp-mode)
  (message "You are in Emacs Lisp"))
 (t
  (message "You are somewhere else")))
</pre>
</div>

<p>
Some programmers may argue that <code>pcase</code> is already more elegant. I
think it is true in this specific example, though I remain flexible
and practical: I will use whatever makes more sense for the code I am
writing. While on the topic of elegance, I should inform you that
practically all of the conditional logic can be done in a way that may
seem unexpected. Consider how my examples in this book make repetitive
use of <code>message</code>, when in reality the only part that changes is the
actual string/argument passed to that function. If we care about
&ldquo;elegance&rdquo;, we might prefer this way of expressing ourselves:
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">(message
 (pcase major-mode
   ('org-mode "You are in Org")
   ('emacs-lisp-mode "You are in Emacs Lisp")
   (_ "You are somewhere else")))
</pre>
</div>

<p>
Same idea for <code>if</code>, <code>when</code>, and the rest. It works, but now you have
to keep in your head the <code>message</code> that is waiting for the <code>pcase</code>
that is testing for many conditions.
</p>

<p>
Back to the topic of what <code>pcase</code> does differently. If you read its
documentation, you will realise that it has its own mini language, or
&ldquo;domain-specific language&rdquo; (DSL). This is common for macros
(<a href="#h:evaluation-inside-of-a-macro-or-special-form">Evaluation inside of a macro or special form</a>). They define how
evaluation is done and what sort of expressions are treated specially.
Let me then gift you this toy function that illustrates some of the
main features of the DSL now under consideration:
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">(defun my-toy-pcase (argument)
  "Use `pcase' to return an appropriate response for ARGUMENT."
  (pcase argument
    (`(,one ,_ ,three)
     (message "List where first element is `%s', second is ignored, third is `%s'" one three))
    (`(,one . ,two)
     (message "Cons cell where first element is `%s' and second is `%s'" one two))
    ((pred stringp)
     (message "The argument is a string of some sort"))
    ('hello
     (message "The argument is equal to the symbol `hello'"))
    (_ (message "This is the fallback"))))
</pre>
</div>

<p>
Go ahead and evaluate that function and then try it out (<a href="#h:evaluate-emacs-lisp">Evaluate Emacs Lisp</a>).
What it showcases is the various ways we write the cases that <code>pcase</code>
checks for. You wil notice the use of the backtick and the comma,
which is similar to the quasi quoting I have covered before (<a href="#h:partial-evaluation-inside-of-a-list">Evaluate some elements inside of a list</a>).
Those perform the pattern matching against the expression that is
given to <code>pcase</code>, in this case the <code>argument</code>. If the pattern matches,
the elements of the list are bound to the corresponding elements
returned by <code>argument</code>. Then they can be used inside of that lexical
scope (<a href="#h:control-flow-with-if-let-and-friends">Control flow with <code>if-let*</code> and friends</a>).
</p>

<p>
Below are a couple of examples with <code>my-toy-pcase</code>:
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">(my-toy-pcase '("Protesilaos" "of" "Cyprus"))
;; =&gt; "List where first element is â€˜Protesilaosâ€™, second is ignored, third is â€˜Cyprusâ€™"

(my-toy-pcase '("Protesilaos" . "Cyprus"))
;; =&gt; "Cons cell where first element is â€˜Protesilaosâ€™ and second is â€˜Cyprusâ€™"
</pre>
</div>

<p>
Some of those clauses in <code>my-toy-pcase</code> are a different way to express
<code>cond</code>. Arguably better, but not a clear winner in my opinion. What is
impressive and a true paradigm shift is the concept of
&ldquo;destructuring&rdquo;, else the pattern matching done to the expression that
effectively <code>let</code> binds elements of a list or cons cell to their
corresponding index in the list. The syntax used for this
destructuring is arcane, until you relate it to the backtick and the
comma which are used for selective evaluation (<a href="#h:partial-evaluation-inside-of-a-list">Evaluate some elements inside of a list</a>).
</p>

<p>
With this in mind, consider <code>pcase-let</code>, <code>pcase-let*</code>, <code>pcase-lambda</code>,
and <code>pcase-dolist</code>, as variations of the plain <code>let</code>, <code>let*</code>,
<code>lambda</code>, and <code>dolist</code> with the added feature of supporting
destructuring. They are not doing any of the extras of <code>pcase</code>
though&#x2014;just destructuring on top of their familiar behaviour!
Perhaps calling something <code>pcase-let</code> when it does not handle cases is
wrong, but you will eventually get used to the <code>pcase-</code> prefix being a
reference to destructuring capabilities. Those are especially useful
when you are working with the return value of a function which comes
as a list.
</p>

<p>
Consider a <code>pcase-dolist</code>. It maps through a list of elements for side
effects only, just like <code>dolist</code>, while doing destructuring (<a href="#h:mapping-through-a-list-of-elements">Mapping through a list of elements</a>).
For example, evaluate the following <code>defvar</code>, then the <code>pcase-dolist</code>,
and then check the <code>*Messages*</code> buffer to confirm what it did. Once
you do that, come back and evaluate the <code>dolist</code> and check the
<code>*Messages*</code> once more.
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">(defvar my-sample-data-for-pcase-dolist
  '(("Craig Gordon" "Goalkeeper" "Hearts")
    ("Aaron Hickey" "Defender" "Brentford")
    ("Grant Hanley" "Defender" "Hibernian")
    ("Scott McKenna" "Defender" "GNK Dinamo")
    ("Andy Robertson" "Defender" "Liverpool")
    ("Lewis Ferguson" "Midfielder" "Bologna")
    ("John McGinn" "Midfielder" "Aston Villa")
    ("Scott McTominay" "Midfielder" "Napoli")
    ("Ben Gannon-Doak" "Forward" "Bournemouth")
    ("Ryan Christie" "Forward" "Bournemouth")
    ("Lyndon Dykes" "Forward" "Birmingham"))
  "The starting lineup of Scotland's final qualifier match for the World Cup 2026.")

;; Here we go through a list of elements for side effects only while
;; relying on destructuring to create the bindings.
(pcase-dolist (`(,name ,position ,club) my-sample-data-for-pcase-dolist)
  (message "%s is a %s who plays for %s" name position club))

;; And here is the equivalent with a regular `dolist' and `let'.
(dolist (element my-sample-data-for-pcase-dolist)
  (let ((name (nth 0 element))
        (position (nth 1 element))
        (club (nth 2 element)))
    (message "%s is a %s who plays for %s" name position club)))
</pre>
</div>

<p>
Whether you use <code>pcase</code> and destructuring in general is up to you. You
do not require them to write high quality code. Though you might agree
with those who consider them inherently more elegant and opt to use
them for this very reason. I do rely on them on occasion, though I am
not fully convinced that destructuring is the superior paradigm. It
makes the code harder to reason about if you are not well versed in
its domain-specific language (<a href="#h:basics-of-how-lisp-works">Basics of how Lisp works</a>). This is
especially true once you work with real-world programs rather than the
simple snippets I am showing you here.
</p>
</div>
</div>
<div id="outline-container-h:run-some-code-or-fall-back-to-some-other-code" class="outline-2">
<h2 id="h:run-some-code-or-fall-back-to-some-other-code"><span class="section-number-2">21.</span> Run some code or fall back to some other code</h2>
<div class="outline-text-2" id="text-h:run-some-code-or-fall-back-to-some-other-code">
<p>
Your typical program will rely on <code>if</code>, <code>cond</code>, and the like for
control flow (<a href="#h:basic-control-flow-with-if-cond-and-others">Basic control flow with <code>if</code>, <code>cond</code>, and others</a>).
Depending on your specific needs or stylistic considerations, it may
even include <code>pcase</code> (<a href="#h:pattern-match-with-pcase-and-related">Pattern match with <code>pcase</code> and related</a>) as well
as <code>if-let*</code> or related (<a href="#h:control-flow-with-if-let-and-friends">Control flow with <code>if-let*</code> and friends</a>).
Whatever style you prefer there are some cases that make it imperative
you run additional code after your primary operation concludes or
exits. The idea is to clean up whatever intermediate state you created
so that any unwanted&#x2014;yet temporarily necessary&#x2014;side effects are
undone (<a href="#h:side-effect-and-return-value">Side effect and return value</a>). The logic is &ldquo;do some
computation with all the desired side effects, then once that
transpires do some other computation to undo the side effects of the
latter or perform whatever kind you reset you consider necessary&rdquo;.
This is the concept of &ldquo;unwinding&rdquo;, which is implemented via
<code>unwind-protect</code>.
</p>

<p>
Here is some pseudo code:
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">(unwind-protect
    ;; First do this
    DOING-STUFF
  ;; Then do this
  UNDOING-STUFF)
</pre>
</div>

<p>
With the above example in mind, <code>DOING-STUFF</code> will be evaluated first
and then <code>UNDOING-STUFF</code> will follow. If <code>DOING-STUFF</code> does not
encounter errors, <code>UNDOING-STUFF</code> will be evaluated in sequence and
the return value of this entire expression will be what <code>DOING-STUFF</code>
returns. If, by contrast, <code>DOING-STUFF</code> exits without doing what it
was supposed to (a &ldquo;non-local exit&rdquo;), then the <code>UNDOING-STUFF</code> will
still be evaluated and there will be no return value. A non-local exit
occurs when the <code>signal</code> function is called to report on some error.
What <code>signal</code> effectively does is abort the current computation and,
depending on the type of the signal like with <code>error</code>, produce a
backtrace. Some signals, like <code>quit</code>, do not produce a backtrace: they
simply abort the current computation. You signal <code>quit</code> whenever you
type C-g (<code>keyboard-quit</code>).
</p>

<p>
To illustrate what I just wrote:
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">;; It does not make sense to use `unwind-protect' if we are not
;; producing side effects, but focus on the return value for now.
(unwind-protect
    (+ 1 1)
  (+ 10 10))
;; =&gt; 2

(unwind-protect
    (progn
      (+ 1 1)
      (signal 'quit (list "We are aborting")))
  (+ 10 10))
;; =&gt; NO RETURN VALUE.
;; =&gt; Quit: "We are aborting"
</pre>
</div>

<p>
In other words, the unwinding part of the logic, which is the <code>(+ 10
10)</code> in this case, is for side effects only: it does not return a
value.
</p>

<p>
Let me now show you when <code>unwinding-protect</code> makes sense. We want to
produce some side effect and then undo it. In the following code
block, I define a function which creates a minibuffer prompt. It asks
you to provide a <code>y</code> or <code>n</code> answer, which is shorthand notation for
&ldquo;yes&rdquo; or &ldquo;no&rdquo;. It tests the return value of <code>y-or-n-p</code> to determine
what it needs to do. While the prompt is waiting for your input, the
function highlights all instances of the regular expression <code>(message</code>
in the current buffer. Those highlights must go away after you are
done with the minibuffer and its consequences. What is in the <code>progn</code>
is the part we want to run and we guard against its side effects with
the following <code>unhighlight-regexp</code>.
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">(defun my-prompt-with-temporary-highlight ()
  "Ask for confirmation and highlight all instances of a regexp while waiting."
  (let ((regexp "(message"))
    (unwind-protect
        (progn
          (highlight-regexp regexp)
          (if (y-or-n-p "Should we proceed or not? ")
              (message "You have decided to proceed")
            (message "You prefer not to continue")))
      (unhighlight-regexp regexp))))

;; Call the following with the above in context.  Check how the
;; regular expression is highlighted.
(my-prompt-with-temporary-highlight)
</pre>
</div>

<p>
Try the above in your Emacs to get a feel for it. Whatever answer you
provide at the prompt, the code still exits locally, meaning that the
<code>progn</code> produces whatever value it is designed to return. As always,
the <code>unhighlight-regexp</code> is evaluated no matter what. To get a
non-local exit from the <code>my-prompt-with-temporary-highlight</code> wait for
the prompt and while it is open type C-g (<code>keyboard-quit</code>).
Whether you exit locally or not, the highlights will be undone, which
is the desired outcome.
</p>

<p>
In practice, your code may encounter an error, which is produced by
the <code>error</code> function (it technically is the <code>signal</code> function with the
<code>error</code> type). When that happens, you are presented with a backtrace
and Emacs enters a &ldquo;recursive edit&rdquo;. The recursive edit means that
unwinding forms are suspended until you either type C-]
(<code>abort-recursive-edit</code>) or select the window with the <code>*Backtrace*</code>
buffer and do q (<code>debugger-quit</code>) from there. Emacs can
enter more than one such recursive edit. While you are inside one, no
more backtrace buffers will be shown: you must first exit the
recursive edit. With default settings, the mode line shows levels of
recursive edits by placing square brackets around the indicator with
the major and minor modes (<a href="#h:what-are-major-and-minor-modes">What are major and minor modes</a>). Always
remember to exit the recursive edit state, otherwise the unwind forms
will not be evaluated and you may end up with an Emacs session that is
in an inconsistent state.
</p>

<p>
To help you get a feel for it, I modify the function I wrote earlier
to now produce an error before it reaches the minibuffer prompt. This
will cause a non-local exit and will create a backtrace if you are not
already in a recursive edit&#x2014;so make sure you exit that one first.
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">(defun my-prompt-with-temporary-highlight-try-with-error ()
  "Ask for confirmation and highlight all instances of a regexp while waiting."
  (let ((regexp "(message"))
    (unwind-protect
        (progn
          (highlight-regexp regexp)
          (error "This error makes no sense here; close the backtrace to test the unwinding")
          (if (y-or-n-p "Should we proceed or not? ")
              (message "You have decided to proceed")
            (message "You prefer not to continue")))
      (unhighlight-regexp regexp))))
</pre>
</div>

<p>
Evaluate this in some buffer that has the <code>(message</code> text and notice
how the unwinding does not happen outright. It happens after you exit
the recursive edit. Again, remember to use <code>abort-recursive-edit</code>.
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">(my-prompt-with-temporary-highlight-try-with-error)
</pre>
</div>

<p>
Taking a step back, you will figure out how <code>unwind-protect</code> is a more
general form of specialists like <code>save-excursion</code> and
<code>save-restriction</code> (<a href="#h:switching-to-another-buffer-window-or-narrowed-state">Switching to another buffer, window, or narrowed state</a>),
while it underpins the <code>save-match-data</code> (<a href="#h:the-match-data-of-the-last-search">The match data of the last search</a>)
among many other functions/macros, such as <code>with-temp-buffer</code> and
<code>save-window-excursion</code>. What <code>unwind-protect</code> does not do is respond
specially to signals, such as those coming from the <code>error</code> function:
it will allow the error to happen, meaning that a backtrace will be
displayed and your code will exit right there (but the unwinding will
still work, as I already explained, once you dismiss the backtrace).
To make your code treat signals in a controlled fashion, such as to
handle an error specially instead of what it normally does, you must
rely on <code>condition-case</code>.
</p>

<p>
With <code>condition-case</code> you assume full control over the behaviour of
your code, including how it should deal with any kind of <code>signal</code>. Put
differently, your Elisp will express the intent of &ldquo;I want to do this,
but if I get a certain signal I want to do that instead&rdquo;. There are
many signals to consider, all of which come from the <code>signal</code>
function. These include the symbols <code>error</code>, <code>user-error</code>,
<code>args-out-of-range</code>, <code>wrong-type-argument</code>, <code>wrong-length-argument</code>,
and <code>quit</code>, in addition to anything else the programmer may choose to
define via <code>define-error</code>. In the following code blocks, I show you
how <code>condition-case</code> looks like. Remember that sometimes you do not do
quoting the usual way because of how the underlying form is
implemented (<a href="#h:evaluation-inside-of-a-macro-or-special-form">Evaluation inside of a macro or special form</a>). The
example I am using is the same I had for <code>unwind-protect</code>.
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">(defun my-prompt-with-temporary-highlight-and-signal-checks ()
  "Ask for confirmation and highlight all instances of a regexp while waiting."
  (let ((regexp "(defun"))
    (condition-case nil
        (progn
          (highlight-regexp regexp)
          (if (y-or-n-p "Should we proceed or not? ")
              (user-error "You have decided to proceed; but we need to return a `user-error' for demo purposes")
            (error "You prefer not to continue; but we need to return an `error' for the demo")))
      (:success
       (unhighlight-regexp regexp)
       (message "No errors, but still need to unwind what we did, plus whatever else we want here"))
      (quit
       (unhighlight-regexp regexp)
       (message "This is our response to the user aborting the prompt"))
      (user-error
       (unhighlight-regexp regexp)
       (message "This is our response to the `user-error' signal"))
      (error
       (unhighlight-regexp regexp)
       (message "This is our response to the `error' signal")))))
</pre>
</div>

<p>
The above function illustrates both the aforementioned concept of
unwinding and the mechanics of handling signals. The abstract
structure of <code>condition-case</code> looks to me like an amalgamation of
<code>let</code>, <code>unwind-protect</code>, and <code>cond</code>. These conditions may include the
special handler of <code>:success</code>, as I show there. Granted, the code I
wrote will never lead to that specific success case, though you can
modify what happens after the prompt to, say, call <code>message</code> instead
of the <code>user-error</code> function, which will then count as a successful
conclusion, i.e. a local exit. Otherwise, I think the expressions I
wrote tell you exactly how this program responds to the signals it
receives.
</p>

<p>
What I have not covered yet, is the aspect of <code>condition-case</code> that is
like the <code>let</code>, namely, how it binds the error data to a variable
within this scope. In my implementation above, it is the <code>nil</code> you see
there, meaning that I choose not to perform such a binding, as I have
no use for its data. Below I decide to bind the error data to the
symbol <code>error-data-i-got</code>, just for the sake of demonstration.
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">(defun my-prompt-with-temporary-highlight-and-signal-checks-with-error-report ()
  "Ask for confirmation and highlight all instances of a regexp while waiting."
  (let ((regexp "(defun"))
    (condition-case error-data-i-got
        (progn
          (highlight-regexp regexp)
          (if (y-or-n-p "Should we proceed or not? ")
              (user-error "You have decided to proceed; but we need to return a `user-error'")
            (error "You prefer not to continue; but we need to return an `error'")))
      (:success
       (unhighlight-regexp regexp)
       (message "No errors, but still need to unwind what we did, plus whatever else we want here")
       (message "The error is `%s' and its data is `%S'" (car error-data-i-got) (cdr error-data-i-got)))
      (quit
       (unhighlight-regexp regexp)
       (message "This is our response to the user aborting the prompt")
       (message "The error is `%s' and its data is `%S'" (car error-data-i-got) (cdr error-data-i-got)))
      (user-error
       (unhighlight-regexp regexp)
       (message "This is our response to the `user-error' signal")
       (message "The error is `%s' and its data is `%S'" (car error-data-i-got) (cdr error-data-i-got)))
      (error
       (unhighlight-regexp regexp)
       (message "This is our response to the `error' signal")
       (message "The error is `%s' and its data is `%S'" (car error-data-i-got) (cdr error-data-i-got))))))
</pre>
</div>

<p>
There will be times when <code>unwind-protect</code> and <code>condition-case</code> are the
right tools for the job. My hope is that these examples have given you
the big picture view and you are now ready to write your own programs
in Emacs Lisp.
</p>
</div>
</div>
<div id="outline-container-h:when-to-use-a-named-function-or-a-lambda-function" class="outline-2">
<h2 id="h:when-to-use-a-named-function-or-a-lambda-function"><span class="section-number-2">22.</span> When to use a named function or a lambda function</h2>
<div class="outline-text-2" id="text-h:when-to-use-a-named-function-or-a-lambda-function">
<p>
The <code>lambda</code> is an anonymous function or, if you will, the form of a
computation as such (<a href="#h:symbols-balanced-expressions-and-quoting">Symbols, balanced expressions, and quoting</a>).
<code>defun</code> defines a function with a given name (&ldquo;eponymous&rdquo;, if I may
bring more of the Greek language into this): technically, it creates
an alias for a <code>lambda</code>. When to use one versus the other is often a
matter of style. Though there are some cases where a certain approach
has clear advantages.
</p>

<p>
The rule of thumb is this: if you need to use the function more than
once, then give it a name and call it by its name. This way, you have
a symbol which provides an indirection to the actual computation: if
you change what the function is doing, the symbol is still the same,
so your changes apply across your code base. Otherwise, you will have
to update all the <code>lambda</code> forms you have.
</p>

<p>
The <code>lambda</code> is anonymous in the sense that it has nothing else
pointing to it: there is no indirection. As such, you must modify the
<code>lambda</code> in place. Knowledge of this quality is of paramount
importance, especially when working with hooks and the advice
mechanism (<a href="#h:hooks-and-the-advice-mechanism">Hooks and the advice mechanism</a>). Unless your code is
trivial, do not use a <code>lambda</code> in a hook or advice as the lack of
indirection makes it harder for you to tweak how your function works.
Rely on a <code>lambda</code> when what you want to do is either trivial and not
subject to further revision or you wish to perform an ad-hoc
computation that is not needed elsewhere.
</p>

<p>
In some cases, you will have a named function that employs a <code>lambda</code>
internally. To modify one of the examples you will find in this book
(<a href="#h:mapping-through-a-list-of-elements">Mapping over a list of elements</a>):
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">(defun my-increment-numbers-by-ten (numbers)
  "Add 10 to each number in NUMBERS and return the new list."
  (mapcar
   (lambda (number)
     (+ 10 number))
   numbers))

(my-increment-numbers-by-ten '(1 2 3))
;; =&gt; (11 12 13)
</pre>
</div>

<p>
A <code>lambda</code> inside of a named function may also be used to do something
over and over again, with the help of <code>let</code>. You may, for instance,
have a function that needs to greet a list of people as a side effect
with <code>mapc</code> and you do not want to define the same function more than
once:
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">(defun my-greet-teams (&amp;rest teams)
  "Say hello to each person in TEAMS and return list with all persons per team.
Each member of TEAMS is a list of strings."
  (let* ((greet-name-fn (lambda (name)
                          (message "Hello %s" name)))
         (greet-team-and-names-fn (lambda (team)
                                    (message "Greeting the team of `%s'..." team)
                                    (mapc greet-name-fn team))))
    (mapcar greet-team-and-names-fn teams)))

(my-greet-teams
 '("PelÃ©" "Ronaldo")
 '("Maradona" "Messi")
 '("Beckenbauer" "Neuer")
 '("Platini" "Zidane")
 '("Baresi" "Maldini")
 '("Eusebio" "Cristiano Ronaldo")
 '("Xavi" "Iniesta")
 '("Charlton" "Shearer")
 '("Cruyff" "Van Basten")
 '("Puskas" "Kubala")
 '("All of the Greece Euro 2004 squad ;)"))
;; =&gt; (("PelÃ©" "Ronaldo") ("Maradona" "Messi") ...)
</pre>
</div>

<p>
The greetings are a side effect in this case and are available in the
<code>*Messages*</code> buffer. You can quickly access that buffer with
C-h e (<code>view-echo-area-messages</code>). It does not really
matter what <code>my-greet-teams</code> is doing. Focus on the combination of a
named function and anonymous functions inside of it.
</p>

<p>
Again, there is no right or wrong. Use a <code>lambda</code> for ad-hoc
computations, where you do not care about the potential for future
edits. Otherwise give your functions a name and you shall benefit from
the indirection they provide.
</p>
</div>
</div>
<div id="outline-container-h:make-your-interactive-function-also-work-from-lisp-calls" class="outline-2">
<h2 id="h:make-your-interactive-function-also-work-from-lisp-calls"><span class="section-number-2">23.</span> Make your interactive function also work from Lisp calls</h2>
<div class="outline-text-2" id="text-h:make-your-interactive-function-also-work-from-lisp-calls">
<p>
Functions can be used interactively when they are declared with the
<code>interactive</code> specification. This turns them into &ldquo;commands&rdquo;
(<a href="#h:autoloading-symbols">Autoloading symbols</a>). They can be called via their name by first
doing M-x (<code>execute-extended-command</code>) and then finding the
command. They may also be assigned to a key and invoked directly by
pressing that key. Unless they absolutely depend on user input, they
will continue to work just fine if called directly from Lisp (<a href="#h:evaluate-emacs-lisp">Evaluate Emacs Lisp</a>).
</p>

<p>
In its simplest form, the <code>interactive</code> specification is an unquoted
list like <code>(interactive)</code>, which looks exactly like a function call
(<a href="#h:basics-of-how-lisp-works">Basics of how Lisp works</a>). Here is a trivial example that calls the
function <code>read-string</code> to produce a minibuffer prompt which accepts
user input and returns it as a string:
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">(defun my-greet-person ()
  (interactive)
  (message "Hello %s" (read-string "Whom to greet? ")))
</pre>
</div>

<p>
The problem with the above implementation is that it is only useful in
interactive use. If you want to issue such a greeting directly through
a program, you need to write another function that does practically
the same thing except that it takes a <code>NAME</code> argument. Like this:
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">(defun my-greet-person-with-name (name)
  "Greet person with NAME."
  (message "Hello %s" name))
</pre>
</div>

<p>
For trivial computations, it is inconsequential whether you have
multiple functions do the work interactively and non-interactively.
But if you are doing something more involved, you do not want to
duplicate it across different functions. It is more maintenance work
for you as the implementation details might change and the subtle
problems shall arise. Emacs gives you the tools to write one function
that can accept arguments when called from Lisp while still working
the way you want when invoked interactively. You can have one
function, with its parameters, which decides how to get the values of
the arguments passed to it depending on if it is called
programmatically or interactively. Consider this scenario:
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">(defun my-greet-interactive-and-non-interactive (name)
  "Greet person with NAME.
When called interactively, produce a minibuffer prompt asking for NAME.

When called from Lisp, NAME is a string."
  (interactive (list (read-string "Whom to greet? ")))
  (message "Hello %s" name))
</pre>
</div>

<p>
The documentation I wrote there tells you exactly what is happening.
Though let me explain <code>interactive</code> in further detail: it takes an
argument, which is a list that mirrors the argument list of the
current <code>defun</code>. In this case, the <code>defun</code> has a list of arguments
that includes a single element, the <code>NAME</code>. Thus, <code>interactive</code> also
has a list with one element, whose value corresponds to <code>NAME</code>. If the
parameters were more than one, then the <code>interactive</code> would have to be
written accordingly: each of its elements would correspond to the
parameter at the same index on the list.
</p>

<p>
This list of expressions you pass to <code>interactive</code> essentially is the
preparatory work that binds values to the parameters. When you call
the above function interactively, you practically tell Emacs that in
this case <code>NAME</code> is the return value of the call to <code>read-string</code>. For
more parameters, you get the same principle but I write it down just
to be clear:
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">(defun my-greet-with-two-parameters (name country)
  "Greet person with NAME from COUNTRY.
When called interactively, produce a minibuffer prompt asking for NAME
and then another prompt for COUNTRY.

When called from Lisp, NAME and COUNTRY are strings."
  (interactive
   (list
    (read-string "Whom to greet? ")
    (read-string "Where from? ")))
  (message "Hello %s of %s" name country))

(my-greet-with-two-parameters "Protesilaos" "Cyprus")
;; =&gt; "Hello Protesilaos of Cyprus"
</pre>
</div>

<p>
The code you write inside of the <code>interactive</code> specification does not
have some special restriction. Express yourself as you see fit with
<code>let</code> bindings and control flow (<a href="#h:basic-control-flow-with-if-cond-and-others">Basic control flow with <code>if</code>, <code>cond</code>, and others</a>).
What matters though is that you return a list whose <code>length</code> is equal
to that of the list of parameters specified by the <code>defun</code>.
</p>

<p>
Of interest in this context is the <code>current-prefix-arg</code> variable: it
holds the value of the prefix argument passed to the current function
in interactive use. Users input it by typing C-u
(<code>universal-argument</code>) for a &ldquo;universal prefix&rdquo; or C-u and
then a number (<code>digit-argument</code>) for a &ldquo;numeric prefix&rdquo; (something
like C-1 is equivalent to C-u followed by
1). There is no predefined behaviour to the prefix
argument: it will do whatever you decide within the <code>interactive</code> by
reading the <code>current-prefix-arg</code> and later in the body of the function
where you parse that value. Here is an example:
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">(defun my-greet-interactive-and-non-interactive-plus-prefix-arg (name &amp;optional be-polite)
  "Greet person with NAME.
With optional BE-POLITE as a prefix argument, use a formal greeting,
otherwise remain casual.

When called interactively, produce a minibuffer prompt asking for NAME.

When called from Lisp, NAME is a string, while BE-POLITE is a non-nil
value."
  (interactive
   (list
    (read-string "Whom to greet? ")
    current-prefix-arg))
  (if be-polite
      (message "Greetings %s" name)
    (message "Howdy %s" name)))

(my-greet-interactive-and-non-interactive-plus-prefix-arg "Prot")
;; =&gt; "Howdy Prot"

(my-greet-interactive-and-non-interactive-plus-prefix-arg "Protesilaos" t)
;; =&gt; "Greetings Protesilaos"
</pre>
</div>

<p>
And here is how you can take it a step further:
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">(defun my-greet-interactive-and-non-interactive-plus-prefix-arg (name &amp;optional title)
  "Greet person with NAME.
With optional TITLE as a prefix argument, prompt for a formal title and
prepend it to NAME.

When called from Lisp, NAME and TITLE are strings."
  (interactive
   (list
    (read-string "Whom to greet? ")
    (when current-prefix-arg
      (read-string "Which title to use? "))))
  (if title
      (message "Greetings %s %s" title name)
    (message "Howdy %s" name)))

(my-greet-interactive-and-non-interactive-plus-prefix-arg "Prot")
;; =&gt; "Howdy Prot"

(my-greet-interactive-and-non-interactive-plus-prefix-arg "Protesilaos" "Teacher")
;; =&gt; "Greetings Teacher Protesilaos"
</pre>
</div>

<p>
Call <code>my-greet-interactive-and-non-interactive-plus-prefix-arg</code> with
and without a prefix argument to get a feel for it. It should do the
same as what it does in the Lisp calls included in this code block.
</p>

<p>
The <code>interactive</code> specification is a powerful mechanism when used
correctly. Do it right and you will end up with a rich corpus of
functions that are equally useful to programs and to users.
</p>
</div>
</div>
<div id="outline-container-h:emacs-as-a-computing-environment" class="outline-2">
<h2 id="h:emacs-as-a-computing-environment"><span class="section-number-2">24.</span> Emacs as a computing environment</h2>
<div class="outline-text-2" id="text-h:emacs-as-a-computing-environment">
<p>
When you start using Emacs, you experience a text editor. You move the
cursor around and insert text. This is basically it. At the surface
level, Emacs looks like every other generic editor you have seen
before. Other actions, such as selecting another window or switching
to a new buffer, are ancillary to text editing. Using Emacs in this
capacity is perfectly fine.
</p>

<p>
What makes Emacs something far more powerful than a text editor is its
ability to be reprogrammed, else &ldquo;extended&rdquo;, live by evaluating some
Emacs Lisp (<a href="#h:basics-of-how-lisp-works">Basics of how Lisp works</a>). Once you install a few packages
or, better, learn to program in Emacs Lisp, your text editor will
become a text-centric interface to increasingly more parts of your
computing life. For example, if you are a programmer, you will
eventually incorporate into your Emacs-based workflow tools for
compiling your project, debugging your code, viewing changes, and
sending patches. If you only write prose, you get to streamline your
work for taking notes, maintaining an agenda, writing technical
documents, sending emails, and more. The best part is that you can
have your integrated programming setup and your plain text writing
platform plus, generally, everything you can use Emacs for, all in
one.
</p>

<p>
With Emacs as your primary interface, you benefit from the consistency
and predictability of a unified system. You have pieced it together so
you understand how it works, more or less. This means you can extend
it when needed. Since Emacs is free software, you retain the freedom
to modify it as you see fit, such as in response to evolving work
requirements. If you have something that does what you expect, you
will rely on it long-term. Emacs will grow or shrink to fit your
current needs.
</p>

<p>
The consistency is one of presentation and function. At the level of
appearances, you get to use the same font and theme settings, as well
as the standard Emacs paradigms of frames, windows, buffers, the
minibuffer, and text editing. On the functionality front, you rely on
one programming language&#x2014;Emacs Lisp&#x2014;for all the interfaces you
will be working with. As such, commands you have written or received
from a package will be available to you in whatever new interface you
bring into your setup. For example, if you have a minor mode that
blinks the current line after a few seconds of idleness (e.g. because
you want to be reminded where you are), this feature is already
available when you start writing emails from inside Emacs: you do not
need to port it the way you would if you were introducing an entirely
new program to your workflow.
</p>

<p>
Compare this potential for unencumbered extensibility with what you
would otherwise be doing. You would be relying on disparate
applications that (i) do not necessarily talk to each other, (ii) each
has its own concepts for the user experience, and (iii) each uses its
own programming or configuration language. You can still get something
decent if you are determined. Though you will realise that Emacs
offers a better return on investment. For instance, the programming
skills you acquire from extending your text editing apply directly to
any future project to further extend Emacs. This is the gift that
keeps giving.
</p>

<p>
As such, you will benefit from thinking of Emacs as (i) the layer of
interactivity on top of command-line tools like <code>ls</code> and <code>grep</code>, and
(ii) an interpreter of a programming language that can draw linkages
between such tools, plus everything else it can do in its own right.
In effect, Emacs is a turbocharged command-line interpreter like Bash
equipped with a fully realised model of user interaction. Your Emacs
Lisp programs are the equivalent of Bash scripts, plus all the
accoutrements of interactivity. The counterpart to <code>#!/bin/bash</code>
(which tells the computer which interpreter is needed for the given
script) is the current Emacs process.
</p>

<p>
Use this concept to better understand what the value proposition of
Emacs is for you in present time and in terms of sheer potential. Have
fun!
</p>
</div>
</div>
<div id="outline-container-h:acknowledgements" class="outline-2">
<h2 id="h:acknowledgements"><span class="section-number-2">25.</span> Acknowledgements</h2>
<div class="outline-text-2" id="text-h:acknowledgements">
<p>
Thanks to the following people for fixing typos and spotting other
such mistakes:
</p>

<p>
Laurent Gatto, Nicolas Semrau, slackline.
</p>
</div>
</div>
<div id="outline-container-h:copying" class="outline-2">
<h2 id="h:copying"><span class="section-number-2">26.</span> COPYING</h2>
<div class="outline-text-2" id="text-h:copying">
<p>
Copyright (C) 2025 Protesilaos Stavrou
</p>

<blockquote>
<p>
Permission is granted to copy, distribute and/or modify this document
under the terms of the GNU Free Documentation License, Version 1.3 or
any later version published by the Free Software Foundation; with no
Invariant Sections, with the Front-Cover Texts being â€œA GNU Manual,â€ and
with the Back-Cover Texts as in (a) below.  A copy of the license is
included in the section entitled â€œGNU Free Documentation License.â€
</p>

<p>
(a) The FSFâ€™s Back-Cover Text is: â€œYou have the freedom to copy and
modify this GNU manual.â€
</p>
</blockquote>
</div>
</div>
<div id="outline-container-h:gnu-free-documentation-license" class="outline-2">
<h2 id="h:gnu-free-documentation-license"><span class="section-number-2">27.</span> GNU Free Documentation License</h2>
<div class="outline-text-2" id="text-h:gnu-free-documentation-license">
<pre>

                GNU Free Documentation License
                 Version 1.3, 3 November 2008


 Copyright (C) 2000, 2001, 2002, 2007, 2008 Free Software Foundation, Inc.
     <https://fsf.org/>
 Everyone is permitted to copy and distribute verbatim copies
 of this license document, but changing it is not allowed.

0. PREAMBLE

The purpose of this License is to make a manual, textbook, or other
functional and useful document "free" in the sense of freedom: to
assure everyone the effective freedom to copy and redistribute it,
with or without modifying it, either commercially or noncommercially.
Secondarily, this License preserves for the author and publisher a way
to get credit for their work, while not being considered responsible
for modifications made by others.

This License is a kind of "copyleft", which means that derivative
works of the document must themselves be free in the same sense.  It
complements the GNU General Public License, which is a copyleft
license designed for free software.

We have designed this License in order to use it for manuals for free
software, because free software needs free documentation: a free
program should come with manuals providing the same freedoms that the
software does.  But this License is not limited to software manuals;
it can be used for any textual work, regardless of subject matter or
whether it is published as a printed book.  We recommend this License
principally for works whose purpose is instruction or reference.


1. APPLICABILITY AND DEFINITIONS

This License applies to any manual or other work, in any medium, that
contains a notice placed by the copyright holder saying it can be
distributed under the terms of this License.  Such a notice grants a
world-wide, royalty-free license, unlimited in duration, to use that
work under the conditions stated herein.  The "Document", below,
refers to any such manual or work.  Any member of the public is a
licensee, and is addressed as "you".  You accept the license if you
copy, modify or distribute the work in a way requiring permission
under copyright law.

A "Modified Version" of the Document means any work containing the
Document or a portion of it, either copied verbatim, or with
modifications and/or translated into another language.

A "Secondary Section" is a named appendix or a front-matter section of
the Document that deals exclusively with the relationship of the
publishers or authors of the Document to the Document's overall
subject (or to related matters) and contains nothing that could fall
directly within that overall subject.  (Thus, if the Document is in
part a textbook of mathematics, a Secondary Section may not explain
any mathematics.)  The relationship could be a matter of historical
connection with the subject or with related matters, or of legal,
commercial, philosophical, ethical or political position regarding
them.

The "Invariant Sections" are certain Secondary Sections whose titles
are designated, as being those of Invariant Sections, in the notice
that says that the Document is released under this License.  If a
section does not fit the above definition of Secondary then it is not
allowed to be designated as Invariant.  The Document may contain zero
Invariant Sections.  If the Document does not identify any Invariant
Sections then there are none.

The "Cover Texts" are certain short passages of text that are listed,
as Front-Cover Texts or Back-Cover Texts, in the notice that says that
the Document is released under this License.  A Front-Cover Text may
be at most 5 words, and a Back-Cover Text may be at most 25 words.

A "Transparent" copy of the Document means a machine-readable copy,
represented in a format whose specification is available to the
general public, that is suitable for revising the document
straightforwardly with generic text editors or (for images composed of
pixels) generic paint programs or (for drawings) some widely available
drawing editor, and that is suitable for input to text formatters or
for automatic translation to a variety of formats suitable for input
to text formatters.  A copy made in an otherwise Transparent file
format whose markup, or absence of markup, has been arranged to thwart
or discourage subsequent modification by readers is not Transparent.
An image format is not Transparent if used for any substantial amount
of text.  A copy that is not "Transparent" is called "Opaque".

Examples of suitable formats for Transparent copies include plain
ASCII without markup, Texinfo input format, LaTeX input format, SGML
or XML using a publicly available DTD, and standard-conforming simple
HTML, PostScript or PDF designed for human modification.  Examples of
transparent image formats include PNG, XCF and JPG.  Opaque formats
include proprietary formats that can be read and edited only by
proprietary word processors, SGML or XML for which the DTD and/or
processing tools are not generally available, and the
machine-generated HTML, PostScript or PDF produced by some word
processors for output purposes only.

The "Title Page" means, for a printed book, the title page itself,
plus such following pages as are needed to hold, legibly, the material
this License requires to appear in the title page.  For works in
formats which do not have any title page as such, "Title Page" means
the text near the most prominent appearance of the work's title,
preceding the beginning of the body of the text.

The "publisher" means any person or entity that distributes copies of
the Document to the public.

A section "Entitled XYZ" means a named subunit of the Document whose
title either is precisely XYZ or contains XYZ in parentheses following
text that translates XYZ in another language.  (Here XYZ stands for a
specific section name mentioned below, such as "Acknowledgements",
"Dedications", "Endorsements", or "History".)  To "Preserve the Title"
of such a section when you modify the Document means that it remains a
section "Entitled XYZ" according to this definition.

The Document may include Warranty Disclaimers next to the notice which
states that this License applies to the Document.  These Warranty
Disclaimers are considered to be included by reference in this
License, but only as regards disclaiming warranties: any other
implication that these Warranty Disclaimers may have is void and has
no effect on the meaning of this License.

2. VERBATIM COPYING

You may copy and distribute the Document in any medium, either
commercially or noncommercially, provided that this License, the
copyright notices, and the license notice saying this License applies
to the Document are reproduced in all copies, and that you add no
other conditions whatsoever to those of this License.  You may not use
technical measures to obstruct or control the reading or further
copying of the copies you make or distribute.  However, you may accept
compensation in exchange for copies.  If you distribute a large enough
number of copies you must also follow the conditions in section 3.

You may also lend copies, under the same conditions stated above, and
you may publicly display copies.


3. COPYING IN QUANTITY

If you publish printed copies (or copies in media that commonly have
printed covers) of the Document, numbering more than 100, and the
Document's license notice requires Cover Texts, you must enclose the
copies in covers that carry, clearly and legibly, all these Cover
Texts: Front-Cover Texts on the front cover, and Back-Cover Texts on
the back cover.  Both covers must also clearly and legibly identify
you as the publisher of these copies.  The front cover must present
the full title with all words of the title equally prominent and
visible.  You may add other material on the covers in addition.
Copying with changes limited to the covers, as long as they preserve
the title of the Document and satisfy these conditions, can be treated
as verbatim copying in other respects.

If the required texts for either cover are too voluminous to fit
legibly, you should put the first ones listed (as many as fit
reasonably) on the actual cover, and continue the rest onto adjacent
pages.

If you publish or distribute Opaque copies of the Document numbering
more than 100, you must either include a machine-readable Transparent
copy along with each Opaque copy, or state in or with each Opaque copy
a computer-network location from which the general network-using
public has access to download using public-standard network protocols
a complete Transparent copy of the Document, free of added material.
If you use the latter option, you must take reasonably prudent steps,
when you begin distribution of Opaque copies in quantity, to ensure
that this Transparent copy will remain thus accessible at the stated
location until at least one year after the last time you distribute an
Opaque copy (directly or through your agents or retailers) of that
edition to the public.

It is requested, but not required, that you contact the authors of the
Document well before redistributing any large number of copies, to
give them a chance to provide you with an updated version of the
Document.


4. MODIFICATIONS

You may copy and distribute a Modified Version of the Document under
the conditions of sections 2 and 3 above, provided that you release
the Modified Version under precisely this License, with the Modified
Version filling the role of the Document, thus licensing distribution
and modification of the Modified Version to whoever possesses a copy
of it.  In addition, you must do these things in the Modified Version:

A. Use in the Title Page (and on the covers, if any) a title distinct
   from that of the Document, and from those of previous versions
   (which should, if there were any, be listed in the History section
   of the Document).  You may use the same title as a previous version
   if the original publisher of that version gives permission.
B. List on the Title Page, as authors, one or more persons or entities
   responsible for authorship of the modifications in the Modified
   Version, together with at least five of the principal authors of the
   Document (all of its principal authors, if it has fewer than five),
   unless they release you from this requirement.
C. State on the Title page the name of the publisher of the
   Modified Version, as the publisher.
D. Preserve all the copyright notices of the Document.
E. Add an appropriate copyright notice for your modifications
   adjacent to the other copyright notices.
F. Include, immediately after the copyright notices, a license notice
   giving the public permission to use the Modified Version under the
   terms of this License, in the form shown in the Addendum below.
G. Preserve in that license notice the full lists of Invariant Sections
   and required Cover Texts given in the Document's license notice.
H. Include an unaltered copy of this License.
I. Preserve the section Entitled "History", Preserve its Title, and add
   to it an item stating at least the title, year, new authors, and
   publisher of the Modified Version as given on the Title Page.  If
   there is no section Entitled "History" in the Document, create one
   stating the title, year, authors, and publisher of the Document as
   given on its Title Page, then add an item describing the Modified
   Version as stated in the previous sentence.
J. Preserve the network location, if any, given in the Document for
   public access to a Transparent copy of the Document, and likewise
   the network locations given in the Document for previous versions
   it was based on.  These may be placed in the "History" section.
   You may omit a network location for a work that was published at
   least four years before the Document itself, or if the original
   publisher of the version it refers to gives permission.
K. For any section Entitled "Acknowledgements" or "Dedications",
   Preserve the Title of the section, and preserve in the section all
   the substance and tone of each of the contributor acknowledgements
   and/or dedications given therein.
L. Preserve all the Invariant Sections of the Document,
   unaltered in their text and in their titles.  Section numbers
   or the equivalent are not considered part of the section titles.
M. Delete any section Entitled "Endorsements".  Such a section
   may not be included in the Modified Version.
N. Do not retitle any existing section to be Entitled "Endorsements"
   or to conflict in title with any Invariant Section.
O. Preserve any Warranty Disclaimers.

If the Modified Version includes new front-matter sections or
appendices that qualify as Secondary Sections and contain no material
copied from the Document, you may at your option designate some or all
of these sections as invariant.  To do this, add their titles to the
list of Invariant Sections in the Modified Version's license notice.
These titles must be distinct from any other section titles.

You may add a section Entitled "Endorsements", provided it contains
nothing but endorsements of your Modified Version by various
parties--for example, statements of peer review or that the text has
been approved by an organization as the authoritative definition of a
standard.

You may add a passage of up to five words as a Front-Cover Text, and a
passage of up to 25 words as a Back-Cover Text, to the end of the list
of Cover Texts in the Modified Version.  Only one passage of
Front-Cover Text and one of Back-Cover Text may be added by (or
through arrangements made by) any one entity.  If the Document already
includes a cover text for the same cover, previously added by you or
by arrangement made by the same entity you are acting on behalf of,
you may not add another; but you may replace the old one, on explicit
permission from the previous publisher that added the old one.

The author(s) and publisher(s) of the Document do not by this License
give permission to use their names for publicity for or to assert or
imply endorsement of any Modified Version.


5. COMBINING DOCUMENTS

You may combine the Document with other documents released under this
License, under the terms defined in section 4 above for modified
versions, provided that you include in the combination all of the
Invariant Sections of all of the original documents, unmodified, and
list them all as Invariant Sections of your combined work in its
license notice, and that you preserve all their Warranty Disclaimers.

The combined work need only contain one copy of this License, and
multiple identical Invariant Sections may be replaced with a single
copy.  If there are multiple Invariant Sections with the same name but
different contents, make the title of each such section unique by
adding at the end of it, in parentheses, the name of the original
author or publisher of that section if known, or else a unique number.
Make the same adjustment to the section titles in the list of
Invariant Sections in the license notice of the combined work.

In the combination, you must combine any sections Entitled "History"
in the various original documents, forming one section Entitled
"History"; likewise combine any sections Entitled "Acknowledgements",
and any sections Entitled "Dedications".  You must delete all sections
Entitled "Endorsements".


6. COLLECTIONS OF DOCUMENTS

You may make a collection consisting of the Document and other
documents released under this License, and replace the individual
copies of this License in the various documents with a single copy
that is included in the collection, provided that you follow the rules
of this License for verbatim copying of each of the documents in all
other respects.

You may extract a single document from such a collection, and
distribute it individually under this License, provided you insert a
copy of this License into the extracted document, and follow this
License in all other respects regarding verbatim copying of that
document.


7. AGGREGATION WITH INDEPENDENT WORKS

A compilation of the Document or its derivatives with other separate
and independent documents or works, in or on a volume of a storage or
distribution medium, is called an "aggregate" if the copyright
resulting from the compilation is not used to limit the legal rights
of the compilation's users beyond what the individual works permit.
When the Document is included in an aggregate, this License does not
apply to the other works in the aggregate which are not themselves
derivative works of the Document.

If the Cover Text requirement of section 3 is applicable to these
copies of the Document, then if the Document is less than one half of
the entire aggregate, the Document's Cover Texts may be placed on
covers that bracket the Document within the aggregate, or the
electronic equivalent of covers if the Document is in electronic form.
Otherwise they must appear on printed covers that bracket the whole
aggregate.


8. TRANSLATION

Translation is considered a kind of modification, so you may
distribute translations of the Document under the terms of section 4.
Replacing Invariant Sections with translations requires special
permission from their copyright holders, but you may include
translations of some or all Invariant Sections in addition to the
original versions of these Invariant Sections.  You may include a
translation of this License, and all the license notices in the
Document, and any Warranty Disclaimers, provided that you also include
the original English version of this License and the original versions
of those notices and disclaimers.  In case of a disagreement between
the translation and the original version of this License or a notice
or disclaimer, the original version will prevail.

If a section in the Document is Entitled "Acknowledgements",
"Dedications", or "History", the requirement (section 4) to Preserve
its Title (section 1) will typically require changing the actual
title.


9. TERMINATION

You may not copy, modify, sublicense, or distribute the Document
except as expressly provided under this License.  Any attempt
otherwise to copy, modify, sublicense, or distribute it is void, and
will automatically terminate your rights under this License.

However, if you cease all violation of this License, then your license
from a particular copyright holder is reinstated (a) provisionally,
unless and until the copyright holder explicitly and finally
terminates your license, and (b) permanently, if the copyright holder
fails to notify you of the violation by some reasonable means prior to
60 days after the cessation.

Moreover, your license from a particular copyright holder is
reinstated permanently if the copyright holder notifies you of the
violation by some reasonable means, this is the first time you have
received notice of violation of this License (for any work) from that
copyright holder, and you cure the violation prior to 30 days after
your receipt of the notice.

Termination of your rights under this section does not terminate the
licenses of parties who have received copies or rights from you under
this License.  If your rights have been terminated and not permanently
reinstated, receipt of a copy of some or all of the same material does
not give you any rights to use it.


10. FUTURE REVISIONS OF THIS LICENSE

The Free Software Foundation may publish new, revised versions of the
GNU Free Documentation License from time to time.  Such new versions
will be similar in spirit to the present version, but may differ in
detail to address new problems or concerns.  See
https://www.gnu.org/licenses/.

Each version of the License is given a distinguishing version number.
If the Document specifies that a particular numbered version of this
License "or any later version" applies to it, you have the option of
following the terms and conditions either of that specified version or
of any later version that has been published (not as a draft) by the
Free Software Foundation.  If the Document does not specify a version
number of this License, you may choose any version ever published (not
as a draft) by the Free Software Foundation.  If the Document
specifies that a proxy can decide which future versions of this
License can be used, that proxy's public statement of acceptance of a
version permanently authorizes you to choose that version for the
Document.

11. RELICENSING

"Massive Multiauthor Collaboration Site" (or "MMC Site") means any
World Wide Web server that publishes copyrightable works and also
provides prominent facilities for anybody to edit those works.  A
public wiki that anybody can edit is an example of such a server.  A
"Massive Multiauthor Collaboration" (or "MMC") contained in the site
means any set of copyrightable works thus published on the MMC site.

"CC-BY-SA" means the Creative Commons Attribution-Share Alike 3.0
license published by Creative Commons Corporation, a not-for-profit
corporation with a principal place of business in San Francisco,
California, as well as future copyleft versions of that license
published by that same organization.

"Incorporate" means to publish or republish a Document, in whole or in
part, as part of another Document.

An MMC is "eligible for relicensing" if it is licensed under this
License, and if all works that were first published under this License
somewhere other than this MMC, and subsequently incorporated in whole or
in part into the MMC, (1) had no cover texts or invariant sections, and
(2) were thus incorporated prior to November 1, 2008.

The operator of an MMC Site may republish an MMC contained in the site
under CC-BY-SA on the same site at any time before August 1, 2009,
provided the MMC is eligible for relicensing.


ADDENDUM: How to use this License for your documents

To use this License in a document you have written, include a copy of
the License in the document and put the following copyright and
license notices just after the title page:

    Copyright (c)  YEAR  YOUR NAME.
    Permission is granted to copy, distribute and/or modify this document
    under the terms of the GNU Free Documentation License, Version 1.3
    or any later version published by the Free Software Foundation;
    with no Invariant Sections, no Front-Cover Texts, and no Back-Cover Texts.
    A copy of the license is included in the section entitled "GNU
    Free Documentation License".

If you have Invariant Sections, Front-Cover Texts and Back-Cover Texts,
replace the "with...Texts." line with this:

    with the Invariant Sections being LIST THEIR TITLES, with the
    Front-Cover Texts being LIST, and with the Back-Cover Texts being LIST.

If you have Invariant Sections without Cover Texts, or some other
combination of the three, merge those two alternatives to suit the
situation.

If your document contains nontrivial examples of program code, we
recommend releasing these examples in parallel under your choice of
free software license, such as the GNU General Public License,
to permit their use in free software.
</pre>

<!--
</div>
</div>
<div id="outline-container-h:indices" class="outline-2">
<h2 id="h:indices"><span class="section-number-2">28.</span> Indices</h2>
<div class="outline-text-2" id="text-h:indices">
</div>
<div id="outline-container-h:function-index" class="outline-3">
<h3 id="h:function-index"><span class="section-number-3">28.1.</span> Function index</h3>
<div class="outline-text-3" id="text-h:function-index">
</div>
</div>
<div id="outline-container-h:variable-index" class="outline-3">
<h3 id="h:variable-index"><span class="section-number-3">28.2.</span> Variable index</h3>
<div class="outline-text-3" id="text-h:variable-index">
</div>
</div>
<div id="outline-container-h:concept-index" class="outline-3">
<h3 id="h:concept-index"><span class="section-number-3">28.3.</span> Concept index</h3>
<div class="outline-text-3" id="text-h:concept-index">
-->
</div>
</div>
</div>
